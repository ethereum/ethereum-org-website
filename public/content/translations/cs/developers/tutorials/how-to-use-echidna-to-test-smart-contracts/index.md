---
title: Jak pou≈æ√≠vat Echidnu k testov√°n√≠ chytr√Ωch kontrakt≈Ø
description: Jak pou≈æ√≠vat Echidnu k automatick√©mu testov√°n√≠ chytr√Ωch kontrakt≈Ø
author: "Trailofbits"
lang: cs
tags:
  [
    "solidity",
    "smart kontrakt √∫ƒçty",
    "bezpeƒçnost",
    "testov√°n√≠",
    "fuzzing"
  ]
skill: advanced
published: 2020-04-10
source: Tvorba bezpeƒçn√Ωch kontrakt≈Ø
sourceUrl: https://github.com/crytic/building-secure-contracts/tree/master/program-analysis/echidna
---

## Instalace {#installation}

Echidnu lze nainstalovat prost≈ôednictv√≠m dockeru nebo pomoc√≠ p≈ôedkompilovan√©ho bin√°rn√≠ho souboru.

### Echidna prost≈ôednictv√≠m dockeru {#echidna-through-docker}

```bash
docker pull trailofbits/eth-security-toolbox
docker run -it -v "$PWD":/home/training trailofbits/eth-security-toolbox
```

_Posledn√≠ p≈ô√≠kaz spust√≠ eth-security-toolbox v dockeru, kter√Ω m√° p≈ô√≠stup k va≈°emu aktu√°ln√≠mu adres√°≈ôi. M≈Ø≈æete mƒõnit soubory z va≈°eho hostitele a spou≈°tƒõt n√°stroje na souborech z dockeru_

Uvnit≈ô dockeru spus≈•te:

```bash
solc-select 0.5.11
cd /home/training
```

### Bin√°rn√≠ soubor {#binary}

[https://github.com/crytic/echidna/releases/tag/v1.4.0.0](https://github.com/crytic/echidna/releases/tag/v1.4.0.0)

## √övod do fuzzingu zalo≈æen√©ho na vlastnostech {#introduction-to-property-based-fuzzing}

Echidna je fuzzer zalo≈æen√Ω na vlastnostech, kter√Ω jsme popsali v na≈°ich p≈ôedchoz√≠ch p≈ô√≠spƒõvc√≠ch na blogu ([1](https://blog.trailofbits.com/2018/03/09/echidna-a-smart-fuzzer-for-ethereum/), [2](https://blog.trailofbits.com/2018/05/03/state-machine-testing-with-echidna/), [3](https://blog.trailofbits.com/2020/03/30/an-echidna-for-all-seasons/)).

### Fuzzing {#fuzzing}

[Fuzzing](https://wikipedia.org/wiki/Fuzzing) je dob≈ôe zn√°m√° technika v bezpeƒçnostn√≠ komunitƒõ. Spoƒç√≠v√° v generov√°n√≠ v√≠ce ƒçi m√©nƒõ n√°hodn√Ωch vstup≈Ø pro nalezen√≠ chyb v programu. Fuzzery pro tradiƒçn√≠ software (jako je [AFL](http://lcamtuf.coredump.cx/afl/) nebo [LibFuzzer](https://llvm.org/docs/LibFuzzer.html)) jsou zn√°m√© jako √∫ƒçinn√© n√°stroje pro hled√°n√≠ chyb.

Kromƒõ ƒçistƒõ n√°hodn√©ho generov√°n√≠ vstup≈Ø existuje mnoho technik a strategi√≠ pro generov√°n√≠ dobr√Ωch vstup≈Ø, vƒçetnƒõ:

- Z√≠sk√°v√°n√≠ zpƒõtn√© vazby z ka≈æd√©ho spu≈°tƒõn√≠ a jej√≠ pou≈æit√≠ k ≈ô√≠zen√≠ generov√°n√≠. Nap≈ô√≠klad, pokud novƒõ vygenerovan√Ω vstup vede k objeven√≠ nov√© cesty, m≈Ø≈æe m√≠t smysl generovat nov√© vstupy, kter√© jsou mu bl√≠zk√©.
- Generov√°n√≠ vstupu respektuj√≠c√≠ho struktur√°ln√≠ omezen√≠. Nap≈ô√≠klad, pokud v√°≈° vstup obsahuje hlaviƒçku s kontroln√≠m souƒçtem, bude m√≠t smysl nechat fuzzer generovat vstup ovƒõ≈ôuj√≠c√≠ kontroln√≠ souƒçet.
- Pou≈æit√≠ zn√°m√Ωch vstup≈Ø pro generov√°n√≠ nov√Ωch vstup≈Ø: pokud m√°te p≈ô√≠stup k velk√©mu souboru dat platn√Ωch vstup≈Ø, v√°≈° fuzzer m≈Ø≈æe z nich generovat nov√© vstupy, m√≠sto aby zaƒç√≠nal generov√°n√≠ od nuly. Ty se obvykle naz√Ωvaj√≠ _seeds_.

### Fuzzing zalo≈æen√Ω na vlastnostech {#property-based-fuzzing}

Echidna pat≈ô√≠ do specifick√© rodiny fuzzer≈Ø: fuzzing zalo≈æen√Ω na vlastnostech, silnƒõ inspirovan√Ω [QuickCheck](https://wikipedia.org/wiki/QuickCheck). Na rozd√≠l od klasick√©ho fuzzeru, kter√Ω se sna≈æ√≠ naj√≠t p√°dy, se Echidna sna≈æ√≠ naru≈°it u≈æivatelem definovan√© invarianty.

V chytr√Ωch kontraktech jsou invarianty funkce v Solidity, kter√© mohou p≈ôedstavovat jak√Ωkoli nespr√°vn√Ω nebo neplatn√Ω stav, kter√©ho m≈Ø≈æe kontrakt dos√°hnout, vƒçetnƒõ:

- Nespr√°vn√° kontrola p≈ô√≠stupu: √∫toƒçn√≠k se stal vlastn√≠kem kontraktu.
- Nespr√°vn√Ω stavov√Ω automat: tokeny mohou b√Ωt p≈ôev√°dƒõny, i kdy≈æ je kontrakt pozastaven.
- Nespr√°vn√° aritmetika: u≈æivatel m≈Ø≈æe zp≈Øsobit podteƒçen√≠ sv√©ho z≈Østatku a z√≠skat neomezen√© mno≈æstv√≠ token≈Ø zdarma.

### Testov√°n√≠ vlastnosti s Echidnou {#testing-a-property-with-echidna}

Pod√≠v√°me se, jak testovat chytr√Ω kontrakt s Echidnou. C√≠lem je n√°sleduj√≠c√≠ chytr√Ω kontrakt [`token.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/token.sol):

```solidity
contract Token{
  mapping(address => uint) public balances;
  function airdrop() public{
      balances[msg.sender] = 1000;
  }
  function consume() public{
      require(balances[msg.sender]>0);
      balances[msg.sender] -= 1;
  }
  function backdoor() public{
      balances[msg.sender] += 1;
  }
}
```

Budeme p≈ôedpokl√°dat, ≈æe tento token mus√≠ m√≠t n√°sleduj√≠c√≠ vlastnosti:

- Ka≈æd√Ω m≈Ø≈æe m√≠t maxim√°lnƒõ 1000 token≈Ø
- Token nelze p≈ôev√©st (nejedn√° se o token ERC20)

### Naps√°n√≠ vlastnosti {#write-a-property}

Vlastnosti Echidny jsou funkce v Solidity. Vlastnost mus√≠:

- Nem√≠t ≈æ√°dn√Ω argument
- Vr√°tit `true`, pokud je √∫spƒõ≈°n√°
- M√≠t jm√©no zaƒç√≠naj√≠c√≠ na `echidna`

Echidna bude:

- Automaticky generovat libovoln√© transakce pro testov√°n√≠ vlastnosti.
- Hl√°sit jak√©koli transakce, kter√© vedou k tomu, ≈æe vlastnost vr√°t√≠ `false` nebo vyhod√≠ chybu.
- Zahodit vedlej≈°√≠ √∫ƒçinky p≈ôi vol√°n√≠ vlastnosti (tzn. pokud vlastnost zmƒõn√≠ stavovou promƒõnnou, je to po testu zahozeno)

N√°sleduj√≠c√≠ vlastnost kontroluje, ≈æe volaj√≠c√≠ nem√° v√≠ce ne≈æ 1000 token≈Ø:

```solidity
function echidna_balance_under_1000() public view returns(bool){
      return balances[msg.sender] <= 1000;
}
```

Pou≈æijte dƒõdiƒçnost k oddƒõlen√≠ va≈°eho kontraktu od va≈°ich vlastnost√≠:

```solidity
contract TestToken is Token{
      function echidna_balance_under_1000() public view returns(bool){
            return balances[msg.sender] <= 1000;
      }
  }
```

[`token.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/token.sol) implementuje vlastnost a dƒõd√≠ z tokenu.

### Inicializace kontraktu {#initiate-a-contract}

Echidna pot≈ôebuje [konstruktor](/developers/docs/smart-contracts/anatomy/#constructor-functions) bez argumentu. Pokud v√°≈° kontrakt pot≈ôebuje specifickou inicializaci, mus√≠te ji prov√©st v konstruktoru.

V Echidnƒõ existuj√≠ nƒõkter√© specifick√© adresy:

- `0x00a329c0648769A73afAc7F9381E08FB43dBEA72`, kter√° vol√° konstruktor.
- `0x10000`, `0x20000` a `0x00a329C0648769a73afAC7F9381e08fb43DBEA70`, kter√© n√°hodnƒõ volaj√≠ ostatn√≠ funkce.

V na≈°em aktu√°ln√≠m p≈ô√≠kladu nepot≈ôebujeme ≈æ√°dnou zvl√°≈°tn√≠ inicializaci, proto je n√°≈° konstruktor pr√°zdn√Ω.

### Spu≈°tƒõn√≠ Echidny {#run-echidna}

Echidna se spou≈°t√≠ pomoc√≠:

```bash
echidna-test contract.sol
```

Pokud contract.sol obsahuje v√≠ce kontrakt≈Ø, m≈Ø≈æete specifikovat c√≠l:

```bash
echidna-test contract.sol --contract MyContract
```

### Shrnut√≠: Testov√°n√≠ vlastnosti {#summary-testing-a-property}

N√°sleduj√≠c√≠ text shrnuje spu≈°tƒõn√≠ Echidny na na≈°em p≈ô√≠kladu:

```solidity
contract TestToken is Token{
    constructor() public {}
        function echidna_balance_under_1000() public view returns(bool){
          return balances[msg.sender] <= 1000;
        }
  }
```

```bash
echidna-test testtoken.sol --contract TestToken
...

echidna_balance_under_1000: failed!üí•
  Call sequence, shrinking (1205/5000):
    airdrop()
    backdoor()

...
```

Echidna zjistila, ≈æe vlastnost je naru≈°ena, pokud je vol√°na funkce `backdoor`.

## Filtrov√°n√≠ funkc√≠ pro vol√°n√≠ bƒõhem fuzzingov√© kampanƒõ {#filtering-functions-to-call-during-a-fuzzing-campaign}

Uk√°≈æeme si, jak filtrovat funkce, kter√© maj√≠ b√Ωt fuzzov√°ny.
C√≠lem je n√°sleduj√≠c√≠ chytr√Ω kontrakt:

```solidity
contract C {
  bool state1 = false;
  bool state2 = false;
  bool state3 = false;
  bool state4 = false;

  function f(uint x) public {
    require(x == 12);
    state1 = true;
  }

  function g(uint x) public {
    require(state1);
    require(x == 8);
    state2 = true;
  }

  function h(uint x) public {
    require(state2);
    require(x == 42);
    state3 = true;
  }

 function i() public {
    require(state3);
    state4 = true;
  }

  function reset1() public {
    state1 = false;
    state2 = false;
    state3 = false;
    return;
  }

  function reset2() public {
    state1 = false;
    state2 = false;
    state3 = false;
    return;
  }

  function echidna_state4() public returns (bool) {
    return (!state4);
  }
}
```

Tento mal√Ω p≈ô√≠klad nut√≠ Echidnu naj√≠t urƒçitou sekvenci transakc√≠ ke zmƒõnƒõ stavov√© promƒõnn√©.
Pro fuzzer je to obt√≠≈æn√© (doporuƒçuje se pou≈æ√≠t n√°stroj pro symbolick√© prov√°dƒõn√≠, jako je [Manticore](https://github.com/trailofbits/manticore)).
M≈Ø≈æeme spustit Echidnu, abychom to ovƒõ≈ôili:

```bash
echidna-test multi.sol
...
echidna_state4: passed! üéâ
Seed: -3684648582249875403
```

### Filtrov√°n√≠ funkc√≠ {#filtering-functions}

Echidna m√° pot√≠≈æe s nalezen√≠m spr√°vn√© sekvence pro testov√°n√≠ tohoto kontraktu, proto≈æe dvƒõ resetovac√≠ funkce (`reset1` a `reset2`) nastav√≠ v≈°echny stavov√© promƒõnn√© na `false`.
M≈Ø≈æeme v≈°ak pou≈æ√≠t speci√°ln√≠ funkci Echidny a buƒè d√°t resetovac√≠ funkce na ƒçernou listinu, nebo na b√≠lou listinu pouze funkce `f`, `g`,
`h` a `i`.

Chcete-li d√°t funkce na ƒçernou listinu, m≈Ø≈æeme pou≈æ√≠t tento konfiguraƒçn√≠ soubor:

```yaml
filterBlacklist: true
filterFunctions: ["reset1", "reset2"]
```

Dal≈°√≠m p≈ô√≠stupem k filtrov√°n√≠ funkc√≠ je vyps√°n√≠ funkc√≠ na b√≠l√© listinƒõ. K tomu m≈Ø≈æeme pou≈æ√≠t tento konfiguraƒçn√≠ soubor:

```yaml
filterBlacklist: false
filterFunctions: ["f", "g", "h", "i"]
```

- `filterBlacklist` je ve v√Ωchoz√≠m nastaven√≠ `true`.
- Filtrov√°n√≠ bude provedeno pouze podle jm√©na (bez parametr≈Ø). Pokud m√°te `f()` a `f(uint256)`, filtr `"f"` bude odpov√≠dat obƒõma funkc√≠m.

### Spu≈°tƒõn√≠ Echidny {#run-echidna-1}

Chcete-li spustit Echidnu s konfiguraƒçn√≠m souborem `blacklist.yaml`:

```bash
echidna-test multi.sol --config blacklist.yaml
...
echidna_state4: failed!üí•
  Call sequence:
    f(12)
    g(8)
    h(42)
    i()
```

Echidna t√©mƒõ≈ô okam≈æitƒõ najde sekvenci transakc√≠, kter√° vlastnost zneplatn√≠.

### Shrnut√≠: Filtrov√°n√≠ funkc√≠ {#summary-filtering-functions}

Echidna m≈Ø≈æe bƒõhem fuzzingov√© kampanƒõ buƒè d√°t funkce na ƒçernou listinu, nebo na b√≠lou listinu pomoc√≠:

```yaml
filterBlacklist: true
filterFunctions: ["f1", "f2", "f3"]
```

```bash
echidna-test contract.sol --config config.yaml
...
```

Echidna spust√≠ fuzzingovou kampa≈à buƒè s funkcemi `f1`, `f2` a `f3` na ƒçern√© listinƒõ, nebo vol√°n√≠m pouze tƒõchto funkc√≠, podle
hodnoty booleovsk√© promƒõnn√© `filterBlacklist`.

## Jak testovat assert v Solidity pomoc√≠ Echidny {#how-to-test-soliditys-assert-with-echidna}

V tomto kr√°tk√©m n√°vodu si uk√°≈æeme, jak pou≈æ√≠vat Echidnu k testov√°n√≠ kontroly tvrzen√≠ (assertions) v kontraktech. P≈ôedpokl√°dejme, ≈æe m√°me kontrakt jako je tento:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    // tmp <= counter
    return (counter - tmp);
  }
}
```

### Naps√°n√≠ tvrzen√≠ (assertion) {#write-an-assertion}

Chceme se ujistit, ≈æe `tmp` je men≈°√≠ nebo rovno `counter` po vr√°cen√≠ jejich rozd√≠lu. Mohli bychom napsat
vlastnost pro Echidnu, ale museli bychom hodnotu `tmp` nƒõkam ulo≈æit. M√≠sto toho bychom mohli pou≈æ√≠t tvrzen√≠ (assertion) jako je toto:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    assert (tmp <= counter);
    return (counter - tmp);
  }
}
```

### Spu≈°tƒõn√≠ Echidny {#run-echidna-2}

Chcete-li povolit testov√°n√≠ selh√°n√≠ tvrzen√≠ (assertion), vytvo≈ôte [konfiguraƒçn√≠ soubor Echidny](https://github.com/crytic/echidna/wiki/Config) `config.yaml`:

```yaml
checkAsserts: true
```

Kdy≈æ spust√≠me tento kontrakt v Echidnƒõ, z√≠sk√°me oƒçek√°van√© v√Ωsledky:

```bash
echidna-test assert.sol --config config.yaml
Analyzing contract: assert.sol:Incrementor
assertion in inc: failed!üí•
  Call sequence, shrinking (2596/5000):
    inc(21711016731996786641919559689128982722488122124807605757398297001483711807488)
    inc(7237005577332262213973186563042994240829374041602535252466099000494570602496)
    inc(86844066927987146567678238756515930889952488499230423029593188005934847229952)

Seed: 1806480648350826486
```

Jak m≈Ø≈æete vidƒõt, Echidna hl√°s√≠ selh√°n√≠ tvrzen√≠ (assertion) ve funkci `inc`. P≈ôid√°n√≠ v√≠ce ne≈æ jednoho tvrzen√≠ (assertion) na funkci je mo≈æn√©, ale Echidna nedok√°≈æe ≈ô√≠ct, kter√© tvrzen√≠ selhalo.

### Kdy a jak pou≈æ√≠vat tvrzen√≠ (assertions) {#when-and-how-use-assertions}

Tvrzen√≠ (assertions) lze pou≈æ√≠t jako alternativu k explicitn√≠m vlastnostem, zejm√©na pokud jsou podm√≠nky ke kontrole p≈ô√≠mo spojeny se spr√°vn√Ωm pou≈æit√≠m nƒõjak√© operace `f`. P≈ôid√°n√≠ tvrzen√≠ (assertions) za nƒõjak√Ω k√≥d vynut√≠, ≈æe kontrola probƒõhne okam≈æitƒõ po jeho vykon√°n√≠:

```solidity
function f(..) public {
    // nƒõjak√Ω slo≈æit√Ω k√≥d
    ...
    assert (condition);
    ...
}

```

Naopak, pou≈æit√≠ explicitn√≠ vlastnosti echidna bude n√°hodnƒõ prov√°dƒõt transakce a neexistuje snadn√Ω zp≈Øsob, jak vynutit, kdy p≈ôesnƒõ bude zkontrolov√°na. St√°le je mo≈æn√© pou≈æ√≠t toto ≈ôe≈°en√≠:

```solidity
function echidna_assert_after_f() public returns (bool) {
    f(..);
    return(condition);
}
```

Existuj√≠ v≈°ak nƒõkter√© probl√©my:

- Sel≈æe, pokud je `f` deklarov√°no jako `internal` nebo `external`.
- Nen√≠ jasn√©, kter√© argumenty by se mƒõly pou≈æ√≠t k vol√°n√≠ `f`.
- Pokud se `f` vr√°t√≠, vlastnost sel≈æe.

Obecnƒõ doporuƒçujeme ≈ô√≠dit se [doporuƒçen√≠m Johna Regehra](https://blog.regehr.org/archives/1091) o tom, jak pou≈æ√≠vat tvrzen√≠ (assertions):

- Nevynucujte ≈æ√°dn√Ω vedlej≈°√≠ √∫ƒçinek bƒõhem kontroly tvrzen√≠. Nap≈ô√≠klad: `assert(ChangeStateAndReturn() == 1)`
- Netvrƒète zjevn√© v√Ωroky. Nap≈ô√≠klad `assert(var >= 0)`, kde `var` je deklarov√°no jako `uint`.

Nakonec, pros√≠m, **nepou≈æ√≠vejte** `require` m√≠sto `assert`, proto≈æe Echidna to nebude schopna detekovat (ale kontrakt se stejnƒõ vr√°t√≠).

### Shrnut√≠: Kontrola tvrzen√≠ (Assertion Checking) {#summary-assertion-checking}

N√°sleduj√≠c√≠ text shrnuje spu≈°tƒõn√≠ Echidny na na≈°em p≈ô√≠kladu:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    assert (tmp <= counter);
    return (counter - tmp);
  }
}
```

```bash
echidna-test assert.sol --config config.yaml
Analyzing contract: assert.sol:Incrementor
assertion in inc: failed!üí•
  Call sequence, shrinking (2596/5000):
    inc(21711016731996786641919559689128982722488122124807605757398297001483711807488)
    inc(7237005577332262213973186563042994240829374041602535252466099000494570602496)
    inc(86844066927987146567678238756515930889952488499230423029593188005934847229952)

Seed: 1806480648350826486
```

Echidna zjistila, ≈æe tvrzen√≠ v `inc` m≈Ø≈æe selhat, pokud je tato funkce vol√°na v√≠cekr√°t s velk√Ωmi argumenty.

## Sbƒõr a √∫prava korpusu Echidna {#collecting-and-modifying-an-echidna-corpus}

Uk√°≈æeme si, jak sb√≠rat a pou≈æ√≠vat korpus transakc√≠ s Echidnou. C√≠lem je n√°sleduj√≠c√≠ chytr√Ω kontrakt [`magic.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/magic.sol):

```solidity
contract C {
  bool value_found = false;
  function magic(uint magic_1, uint magic_2, uint magic_3, uint magic_4) public {
    require(magic_1 == 42);
    require(magic_2 == 129);
    require(magic_3 == magic_4+333);
    value_found = true;
    return;
  }

  function echidna_magic_values() public returns (bool) {
    return !value_found;
  }

}
```

Tento mal√Ω p≈ô√≠klad nut√≠ Echidnu naj√≠t urƒçit√© hodnoty pro zmƒõnu stavov√© promƒõnn√©. Pro fuzzer je to obt√≠≈æn√©
(doporuƒçuje se pou≈æ√≠t n√°stroj pro symbolick√© prov√°dƒõn√≠, jako je [Manticore](https://github.com/trailofbits/manticore)).
M≈Ø≈æeme spustit Echidnu, abychom to ovƒõ≈ôili:

```bash
echidna-test magic.sol
...

echidna_magic_values: passed! üéâ

Seed: 2221503356319272685
```

M≈Ø≈æeme v≈°ak st√°le pou≈æ√≠vat Echidnu ke sbƒõru korpusu p≈ôi spou≈°tƒõn√≠ t√©to fuzzingov√© kampanƒõ.

### Sbƒõr korpusu {#collecting-a-corpus}

Chcete-li povolit sbƒõr korpusu, vytvo≈ôte adres√°≈ô korpusu:

```bash
mkdir corpus-magic
```

A [konfiguraƒçn√≠ soubor Echidny](https://github.com/crytic/echidna/wiki/Config) `config.yaml`:

```yaml
coverage: true
corpusDir: "corpus-magic"
```

Nyn√≠ m≈Ø≈æeme spustit n√°≈° n√°stroj a zkontrolovat shrom√°≈ædƒõn√Ω korpus:

```bash
echidna-test magic.sol --config config.yaml
```

Echidna st√°le nem≈Ø≈æe naj√≠t spr√°vn√© magick√© hodnoty, ale m≈Ø≈æeme se pod√≠vat na korpus, kter√Ω shrom√°≈ædila.
Nap≈ô√≠klad jeden z tƒõchto soubor≈Ø byl:

```json
[
  {
    "_gas'": "0xffffffff",
    "_delay": ["0x13647", "0xccf6"],
    "_src": "00a329c0648769a73afac7f9381e08fb43dbea70",
    "_dst": "00a329c0648769a73afac7f9381e08fb43dbea72",
    "_value": "0x0",
    "_call": {
      "tag": "SolCall",
      "contents": [
        "magic",
        [
          {
            "contents": [
              256,
              "93723985220345906694500679277863898678726808528711107336895287282192244575836"
            ],
            "tag": "AbiUInt"
          },
          {
            "contents": [256, "334"],
            "tag": "AbiUInt"
          },
          {
            "contents": [
              256,
              "68093943901352437066264791224433559271778087297543421781073458233697135179558"
            ],
            "tag": "AbiUInt"
          },
          {
            "tag": "AbiUInt",
            "contents": [256, "332"]
          }
        ]
      ]
    },
    "_gasprice'": "0xa904461f1"
  }
]
```

Je z≈ôejm√©, ≈æe tento vstup nespust√≠ selh√°n√≠ v na≈°√≠ vlastnosti. V dal≈°√≠m kroku si v≈°ak uk√°≈æeme, jak jej pro tento √∫ƒçel upravit.

### Nasazen√≠ korpusu {#seeding-a-corpus}

Echidna pot≈ôebuje pomoc, aby si poradila s funkc√≠ `magic`. Zkop√≠rujeme a uprav√≠me vstup tak, aby pou≈æ√≠val vhodn√©
parametry:

```bash
cp corpus/2712688662897926208.txt corpus/new.txt
```

Uprav√≠me `new.txt` tak, aby volal `magic(42,129,333,0)`. Nyn√≠ m≈Ø≈æeme Echidnu znovu spustit:

```bash
echidna-test magic.sol --config config.yaml
...
echidna_magic_values: failed!üí•
  Call sequence:
    magic(42,129,333,0)


Unique instructions: 142
Unique codehashes: 1
Seed: -7293830866560616537

```

Tentokr√°t zjistila, ≈æe vlastnost je okam≈æitƒõ naru≈°ena.

## Hled√°n√≠ transakc√≠ s vysokou spot≈ôebou paliva {#finding-transactions-with-high-gas-consumption}

Uk√°≈æeme si, jak s Echidnou naj√≠t transakce s vysokou spot≈ôebou paliva. C√≠lem je n√°sleduj√≠c√≠ chytr√Ω kontrakt:

```solidity
contract C {
  uint state;

  function expensive(uint8 times) internal {
    for(uint8 i=0; i < times; i++)
      state = state + i;
  }

  function f(uint x, uint y, uint8 times) public {
    if (x == 42 && y == 123)
      expensive(times);
    else
      state = 0;
  }

  function echidna_test() public returns (bool) {
    return true;
  }

}
```

Zde m≈Ø≈æe m√≠t `expensive` velkou spot≈ôebu paliva.

V souƒçasn√© dobƒõ Echidna v≈ædy pot≈ôebuje vlastnost k testov√°n√≠: zde `echidna_test` v≈ædy vrac√≠ `true`.
M≈Ø≈æeme spustit Echidnu, abychom to ovƒõ≈ôili:

```
echidna-test gas.sol
...
echidna_test: passed! üéâ

Seed: 2320549945714142710
```

### Mƒõ≈ôen√≠ spot≈ôeby paliva {#measuring-gas-consumption}

Chcete-li s Echidnou povolit spot≈ôebu paliva, vytvo≈ôte konfiguraƒçn√≠ soubor `config.yaml`:

```yaml
estimateGas: true
```

V tomto p≈ô√≠kladu tak√© zmen≈°√≠me velikost sekvence transakc√≠, aby byly v√Ωsledky sn√°ze pochopiteln√©:

```yaml
seqLen: 2
estimateGas: true
```

### Spu≈°tƒõn√≠ Echidny {#run-echidna-3}

Jakmile m√°me vytvo≈ôen√Ω konfiguraƒçn√≠ soubor, m≈Ø≈æeme Echidnu spustit takto:

```bash
echidna-test gas.sol --config config.yaml
...
echidna_test: passed! üéâ

f used a maximum of 1333608 gas
  Call sequence:
    f(42,123,249) Gas price: 0x10d5733f0a Time delay: 0x495e5 Block delay: 0x88b2

Unique instructions: 157
Unique codehashes: 1
Seed: -325611019680165325

```

- Zobrazen√© palivo je odhad poskytnut√Ω [HEVM](https://github.com/dapphub/dapptools/tree/master/src/hevm#hevm-).

### Odfiltrov√°n√≠ vol√°n√≠ sni≈æuj√≠c√≠ch spot≈ôebu paliva {#filtering-out-gas-reducing-calls}

V√Ω≈°e uveden√Ω n√°vod **Filtrov√°n√≠ funkc√≠ pro vol√°n√≠ bƒõhem fuzzingov√© kampanƒõ** ukazuje, jak
odebrat nƒõkter√© funkce z testov√°n√≠.  
To m≈Ø≈æe b√Ωt z√°sadn√≠ pro z√≠sk√°n√≠ p≈ôesn√©ho odhadu paliva.
Zva≈æte n√°sleduj√≠c√≠ p≈ô√≠klad:

```solidity
contract C {
  address [] addrs;
  function push(address a) public {
    addrs.push(a);
  }
  function pop() public {
    addrs.pop();
  }
  function clear() public{
    addrs.length = 0;
  }
  function check() public{
    for(uint256 i = 0; i < addrs.length; i++)
      for(uint256 j = i+1; j < addrs.length; j++)
        if (addrs[i] == addrs[j])
          addrs[j] = address(0x0);
  }
  function echidna_test() public returns (bool) {
      return true;
  }
}
```

Pokud m≈Ø≈æe Echidna volat v≈°echny funkce, nenajde snadno transakce s vysok√Ωmi n√°klady na palivo:

```
echidna-test pushpop.sol --config config.yaml
...
pop used a maximum of 10746 gas
...
check used a maximum of 23730 gas
...
clear used a maximum of 35916 gas
...
push used a maximum of 40839 gas
```

Je to proto, ≈æe n√°klady z√°vis√≠ na velikosti `addrs` a n√°hodn√° vol√°n√≠ maj√≠ tendenci ponechat pole t√©mƒõ≈ô pr√°zdn√©.
Vylouƒçen√≠ funkc√≠ `pop` a `clear` n√°m v≈°ak d√°v√° mnohem lep≈°√≠ v√Ωsledky:

```yaml
filterBlacklist: true
filterFunctions: ["pop", "clear"]
```

```
echidna-test pushpop.sol --config config.yaml
...
push used a maximum of 40839 gas
...
check used a maximum of 1484472 gas
```

### Shrnut√≠: Hled√°n√≠ transakc√≠ s vysokou spot≈ôebou paliva {#summary-finding-transactions-with-high-gas-consumption}

Echidna m≈Ø≈æe naj√≠t transakce s vysokou spot≈ôebou paliva pomoc√≠ konfiguraƒçn√≠ volby `estimateGas`:

```yaml
estimateGas: true
```

```bash
echidna-test contract.sol --config config.yaml
...
```

Jakmile bude fuzzingov√° kampa≈à ukonƒçena, Echidna nahl√°s√≠ sekvenci s maxim√°ln√≠ spot≈ôebou paliva pro ka≈ædou funkci.
