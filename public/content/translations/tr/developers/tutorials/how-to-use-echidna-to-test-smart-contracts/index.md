---
title: AkÄ±llÄ± sÃ¶zleÅŸmeleri test etmek iÃ§in Echidna nasÄ±l kullanÄ±lÄ±r
description: AkÄ±llÄ± sÃ¶zleÅŸmeleri otomatik olarak test etmek iÃ§in Echidna nasÄ±l kullanÄ±lÄ±r
author: "Trailofbits"
lang: tr
tags:
  - "solidity"
  - "akÄ±llÄ± kontratlar"
  - "gÃ¼venlik"
  - "test etmek"
  - "bulandÄ±rma"
skill: advanced
published: 2020-04-10
source: GÃ¼venli sÃ¶zleÅŸmeler oluÅŸturmak
sourceUrl: https://github.com/crytic/building-secure-contracts/tree/master/program-analysis/echidna
---

## Kurulum {#installation}

Echidna, docker aracÄ±lÄ±ÄŸÄ±yla veya Ã¶nceden derlenmiÅŸ ikili dosya kullanÄ±larak kurulabilir.

### Docker aracÄ±lÄ±ÄŸÄ±yla Echidna {#echidna-through-docker}

```bash
docker pull trailofbits/eth-security-toolbox
docker run -it -v "$PWD":/home/training trailofbits/eth-security-toolbox
```

_Son komut, geÃ§erli dizininize eriÅŸimi olan bir docker'da eth-security-toolbox'Ä± Ã§alÄ±ÅŸtÄ±rÄ±r. DosyalarÄ± ana makinenizden deÄŸiÅŸtirebilir ve dosyalar Ã¼zerindeki araÃ§larÄ± docker'dan Ã§alÄ±ÅŸtÄ±rabilirsiniz_

Docker'Ä±n iÃ§inde ÅŸunu Ã§alÄ±ÅŸtÄ±rÄ±n:

```bash
solc-select 0.5.11
cd /home/training
```

### Ä°kili {#binary}

[https://github.com/crytic/echidna/releases/tag/v1.4.0.0](https://github.com/crytic/echidna/releases/tag/v1.4.0.0)

## Ã–zellik tabanlÄ± bulandÄ±rmaya giriÅŸ {#introduction-to-property-based-fuzzing}

Echidna, Ã¶nceki blog yazÄ±larÄ±mÄ±zda tanÄ±mladÄ±ÄŸÄ±mÄ±z bir Ã¶zellik tabanlÄ± bulandÄ±rÄ±cÄ±dÄ±r ([1](https://blog.trailofbits.com/2018/03/09/echidna-a-smart-fuzzer-for-ethereum/), [2](https://blog.trailofbits.com/2018/05/03/state-machine-testing-with-echidna/), [3](https://blog.trailofbits.com/2020/03/30/an-echidna-for-all-seasons/)).

### BulandÄ±rma {#fuzzing}

[BulandÄ±rma ](https://wikipedia.org/wiki/Fuzzing) (Fuzzing), gÃ¼venlik topluluÄŸunda iyi bilinen bir tekniktir. Programdaki hatalarÄ± bulmak iÃ§in hemen hemen rastgele girdiler oluÅŸturmayÄ± iÃ§erir. Geleneksel yazÄ±lÄ±m iÃ§in bulandÄ±rÄ±cÄ±lar ([AFL](http://lcamtuf.coredump.cx/afl/) veya [LibFuzzer](https://llvm.org/docs/LibFuzzer.html) gibi) hata tespiti iÃ§in verimli araÃ§lar olarak bilinirler.

Tamamen rastgele girdi oluÅŸturmanÄ±n Ã¶tesinde, iyi girdiler Ã¼retmek iÃ§in aÅŸaÄŸÄ±dakiler de dahil olmak Ã¼zere birÃ§ok teknik ve strateji vardÄ±r:

- Her yÃ¼rÃ¼tmeden geri bildirim alÄ±n ve bunu, oluÅŸturmaya rehberlik etmek iÃ§in kullanÄ±n. Ã–rneÄŸin, yeni oluÅŸturulan bir girdi yeni bir yolun keÅŸfine yol aÃ§Ä±yorsa, ona yakÄ±n yeni girdiler oluÅŸturmak mantÄ±klÄ± olabilir.
- YapÄ±sal bir kÄ±sÄ±tlamaya gÃ¶re girdi oluÅŸturma. Ã–rneÄŸin, girdiniz saÄŸlama toplamÄ± olan bir baÅŸlÄ±k iÃ§eriyorsa, bulandÄ±rÄ±cÄ±nÄ±n saÄŸlama toplamÄ±nÄ± doÄŸrulayan girdi oluÅŸturmasÄ±na izin vermek mantÄ±klÄ± olacaktÄ±r.
- Yeni girdiler oluÅŸturmak iÃ§in bilinen girdileri kullanma: EÄŸer bÃ¼yÃ¼k bir geÃ§erli girdi veri setine eriÅŸiminiz varsa, bulandÄ±rÄ±cÄ±nÄ±z sÄ±fÄ±rdan Ã¼retime baÅŸlamak yerine onlardan yeni girdiler Ã¼retebilir. Bunlara genellikle _tohum_ denir.

### Ã–zellik temelli bulandÄ±rma {#property-based-fuzzing}

Echidna spesifik bir bulandÄ±rÄ±cÄ± ailesine mensuptur: Ã¶zellik temelli bulandÄ±rma Ã§oÄŸunlukla [QuickCheck](https://wikipedia.org/wiki/QuickCheck)'ten ilham almÄ±ÅŸtÄ±r. Ã‡Ã¶kmeleri bulmaya Ã§alÄ±ÅŸan klasik bulandÄ±rÄ±cÄ±nÄ±n aksine Echidna, kullanÄ±cÄ± tanÄ±mlÄ± deÄŸiÅŸmezleri kÄ±rmaya Ã§alÄ±ÅŸacaktÄ±r.

AkÄ±llÄ± sÃ¶zleÅŸmelerde deÄŸiÅŸmezler, sÃ¶zleÅŸmenin ulaÅŸabileceÄŸi herhangi bir yanlÄ±ÅŸ veya geÃ§ersiz durumu temsil edebilen Solidity fonksiyonlarÄ±dÄ±r:

- HatalÄ± eriÅŸim denetimi: SaldÄ±rgan sÃ¶zleÅŸmenin sahibi oldu.
- HatalÄ± durum makinesi: SÃ¶zleÅŸme duraklatÄ±lmÄ±ÅŸken token'lar aktarÄ±labilir.
- HatalÄ± aritmetik: KullanÄ±cÄ± bakiyesini yetersiz gÃ¶sterip sÄ±nÄ±rsÄ±z Ã¼cretsiz token alabilir.

### Echidna ile bir Ã¶zelliÄŸi test etme {#testing-a-property-with-echidna}

Echidna ile akÄ±llÄ± bir sÃ¶zleÅŸmenin nasÄ±l test edileceÄŸini gÃ¶receÄŸiz. Hedef, aÅŸaÄŸÄ±daki akÄ±llÄ± sÃ¶zleÅŸme [`token.sol`'dÃ¼r](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/token.sol):

```solidity
contract Token{
  mapping(address => uint) public balances;
  function airdrop() public{
      balances[msg.sender] = 1000;
  }
  function consume() public{
      require(balances[msg.sender]>0);
      balances[msg.sender] -= 1;
  }
  function backdoor() public{
      balances[msg.sender] += 1;
  }
}
```

Bu token'Ä±n aÅŸaÄŸÄ±daki Ã¶zelliklere sahip olmasÄ± gerektiÄŸi varsayÄ±mÄ±nÄ± yapacaÄŸÄ±z:

- Herkes en fazla 1000 token'a sahip olabilir
- Token transfer edilemez (bir ERC20 token'Ä± deÄŸildir)

### Bir Ã¶zellik yazÄ±n {#write-a-property}

Echidna Ã¶zellikleri, Solidity fonksiyonlarÄ±dÄ±r. Bir Ã¶zellikte ÅŸunlar bulunmalÄ±:

- ArgÃ¼manÄ± olmamalÄ±
- BaÅŸarÄ±lÄ±ysa `true` dÃ¶ndÃ¼rmeli
- AdÄ± `echidna` ile baÅŸlÄ±yor olmalÄ±

Echidna ÅŸunlarÄ± yapacaktÄ±r:

- Ã–zelliÄŸi test etmek iÃ§in otomatik olarak rastgele iÅŸlemler oluÅŸturacak.
- Bir Ã¶zelliÄŸin `false` dÃ¶ndÃ¼rmesine veya bir hata vermesine neden olan tÃ¼m iÅŸlemleri bildirecek.
- Bir Ã¶zelliÄŸi Ã§aÄŸÄ±rÄ±rken yan etkiyi atacak (yani Ã¶zellik bir durum deÄŸiÅŸkenini deÄŸiÅŸtirirse, testten sonra atÄ±lÄ±r)

AÅŸaÄŸÄ±daki Ã¶zellik, Ã§aÄŸÄ±ranÄ±n 1000'den fazla token'a sahip olup olmadÄ±ÄŸÄ±nÄ± kontrol eder:

```solidity
function echidna_balance_under_1000() public view returns(bool){
      return balances[msg.sender] <= 1000;
}
```

SÃ¶zleÅŸmenizi Ã¶zelliklerinizden ayÄ±rmak iÃ§in kalÄ±tÄ±mÄ± kullanÄ±n:

```solidity
contract TestToken is Token{
      function echidna_balance_under_1000() public view returns(bool){
            return balances[msg.sender] <= 1000;
      }
  }
```

[`token.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/token.sol) Ã¶zelliÄŸi uygular ve token'dan kalÄ±tÄ±m gerÃ§ekleÅŸtirir.

### Bir sÃ¶zleÅŸme baÅŸlatÄ±n {#initiate-a-contract}

Echidna, argÃ¼manÄ± olmayan bir [yapÄ±cÄ±ya](/developers/docs/smart-contracts/anatomy/#constructor-functions) ihtiyaÃ§ duyar. SÃ¶zleÅŸmenizin Ã¶zel bir baÅŸlatmaya ihtiyacÄ± varsa, bunu yapÄ±cÄ±da yapmanÄ±z gerekir.

Echidna'da bazÄ± Ã¶zel adresler vardÄ±r:

- `0x00a329c0648769A73afAc7F9381E08FB43dBEA72` yapÄ±cÄ±yÄ± Ã§aÄŸÄ±rÄ±r.
- `0x10000`, `0x20000`, ve `0x00a329C0648769a73afAC7F9381e08fb43DBEA70` diÄŸer fonksiyonlarÄ± rastgele Ã§aÄŸÄ±rÄ±r.

Mevcut Ã¶rneÄŸimizde herhangi bir Ã¶zel baÅŸlatmaya ihtiyacÄ±mÄ±z yok, bu yÃ¼zden yapÄ±cÄ±mÄ±z boÅŸ.

### Echidna'yÄ± Ã§alÄ±ÅŸtÄ±rÄ±n {#run-echidna}

Echidna ÅŸÃ¶yle baÅŸlatÄ±lÄ±r:

```bash
echidna-test contract.sol
```

Contract.sol birden fazla sÃ¶zleÅŸme iÃ§eriyorsa hedefi belirtebilirsiniz:

```bash
echidna-test contract.sol --contract MyContract
```

### Ã–zet: Bir Ã¶zelliÄŸi test etme {#summary-testing-a-property}

AÅŸaÄŸÄ±daki, Ã¶rneÄŸimizde echidna'nÄ±n Ã§alÄ±ÅŸmasÄ±nÄ± Ã¶zetler:

```solidity
contract TestToken is Token{
    constructor() public {}
        function echidna_balance_under_1000() public view returns(bool){
          return balances[msg.sender] <= 1000;
        }
  }
```

```bash
echidna-test testtoken.sol --contract TestToken
...

echidna_balance_under_1000: failed!ğŸ’¥
  Call sequence, shrinking (1205/5000):
    airdrop()
    backdoor()

...
```

Echidna, `backdoor` Ã§aÄŸrÄ±lÄ±rsa Ã¶zelliÄŸin ihlal edildiÄŸini tespit etti.

## Bir bulandÄ±rma iÅŸlemi sÄ±rasÄ±nda Ã§aÄŸrÄ±lacak filtreleme iÅŸlevleri {#filtering-functions-to-call-during-a-fuzzing-campaign}

BulandÄ±rÄ±lacak fonksiyonlarÄ±n nasÄ±l filtreleneceÄŸini gÃ¶receÄŸiz. Hedef, aÅŸaÄŸÄ±daki akÄ±llÄ± sÃ¶zleÅŸmedir:

```solidity
contract C {
  bool state1 = false;
  bool state2 = false;
  bool state3 = false;
  bool state4 = false;

  function f(uint x) public {
    require(x == 12);
    state1 = true;
  }

  function g(uint x) public {
    require(state1);
    require(x == 8);
    state2 = true;
  }

  function h(uint x) public {
    require(state2);
    require(x == 42);
    state3 = true;
  }

 function i() public {
    require(state3);
    state4 = true;
  }

  function reset1() public {
    state1 = false;
    state2 = false;
    state3 = false;
    return;
  }

  function reset2() public {
    state1 = false;
    state2 = false;
    state3 = false;
    return;
  }

  function echidna_state4() public returns (bool) {
    return (!state4);
  }
}
```

Bu kÃ¼Ã§Ã¼k Ã¶rnek, Echidna'yÄ± bir durum deÄŸiÅŸkenini deÄŸiÅŸtirmek iÃ§in belirli bir iÅŸlem dizisini bulmaya zorlar. Bu bir bulandÄ±rÄ±cÄ± iÃ§in zordur ([Manticore](https://github.com/trailofbits/manticore) gibi sembolik bir yÃ¼rÃ¼tme aracÄ± kullanÄ±lmasÄ± Ã¶nerilir). Bunu doÄŸrulamak iÃ§in Echidna'yÄ± Ã§alÄ±ÅŸtÄ±rabiliriz:

```bash
echidna-test multi.sol
...
echidna_state4: passed! ğŸ‰
Seed: -3684648582249875403
```

### FonksiyonlarÄ± filtreleme {#filtering-functions}

Echidna, iki sÄ±fÄ±rlama fonksiyonu (`reset1` ve `reset2`) tÃ¼m durum deÄŸiÅŸkenlerini `false` olarak ayarlayacaÄŸÄ±ndan, bu sÃ¶zleÅŸmeyi test etmek iÃ§in doÄŸru sÄ±rayÄ± bulmakta zorlanÄ±yor. Ancak, sÄ±fÄ±rlama fonksiyonunu kara listeye almak veya yalnÄ±zca `f`, `g`, `h` ve `i` fonksiyonlarÄ±nÄ± beyaz listeye almak iÃ§in Ã¶zel bir Echidna Ã¶zelliÄŸini kullanabiliriz.

Ä°ÅŸlevleri kara listeye almak iÃ§in bu yapÄ±landÄ±rma dosyasÄ±nÄ± kullanabiliriz:

```yaml
filterBlacklist: true
filterFunctions: ["reset1", "reset2"]
```

FonksiyonlarÄ± filtrelemek iÃ§in baÅŸka bir yaklaÅŸÄ±m, beyaz listeye alÄ±nan fonksiyonlarÄ± listelemektir. Bunu yapmak iÃ§in ÅŸu yapÄ±landÄ±rma dosyasÄ±nÄ± kullanabiliriz:

```yaml
filterBlacklist: false
filterFunctions: ["f", "g", "h", "i"]
```

- `filterBlacklist` varsayÄ±lan olarak `true` hÃ¢ldedir.
- Filtreleme sadece ada gÃ¶re yapÄ±lacaktÄ±r (parametreler olmadan). EÄŸer `f()` ve `f(uint256)` varsa, `"f"` filtresi iki fonksiyon ile de eÅŸleÅŸecektir.

### Echidna'yÄ± Ã§alÄ±ÅŸtÄ±rÄ±n {#run-echidna-1}

Echidna'yÄ± bir `blacklist.yaml` yapÄ±landÄ±rma dosyasÄ± ile Ã§alÄ±ÅŸtÄ±rmak iÃ§in:

```bash
echidna-test multi.sol --config blacklist.yaml
...
echidna_state4: failed!ğŸ’¥
  Call sequence:
    f(12)
    g(8)
    h(42)
    i()
```

Echidna, Ã¶zelliÄŸi tahrif edecek iÅŸlemlerin sÄ±rasÄ±nÄ± neredeyse anÄ±nda bulacaktÄ±r.

### Ã–zet: FonksiyonlarÄ± filtreleme {#summary-filtering-functions}

Echidna, aÅŸaÄŸÄ±dakileri kullanarak bulanÄ±klaÅŸtÄ±rma Ã§alÄ±ÅŸmasÄ± sÄ±rasÄ±nda Ã§aÄŸrÄ±lacak fonksiyonlarÄ± kara veya beyaz listeye alabilir:

```yaml
filterBlacklist: true
filterFunctions: ["f1", "f2", "f3"]
```

```bash
echidna-test contract.sol --config config.yaml
...
```

Echidna, `f1`, `f2` ve `f3`'Ã¼ kara listeye alarak veya `filterBlacklist` boolean deÄŸerine gÃ¶re yalnÄ±zca bunlarÄ± Ã§aÄŸÄ±rarak bir bulanÄ±klaÅŸtÄ±rma Ã§alÄ±ÅŸmasÄ± baÅŸlatÄ±r.

## Solidity'nin teyidi Echidna ile nasÄ±l test edilir {#how-to-test-soliditys-assert-with-echidna}

Bu kÄ±sa Ã¶ÄŸreticide, sÃ¶zleÅŸmelerde teyit kontrolÃ¼nÃ¼ test etmek iÃ§in Echidna'nÄ±n nasÄ±l kullanÄ±lacaÄŸÄ±nÄ± gÃ¶stereceÄŸiz. Diyelim ki ÅŸuna benzer bir sÃ¶zleÅŸmemiz var:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    // tmp <= counter
    return (counter - tmp);
  }
}
```

### Bir teyit yazÄ±n {#write-an-assertion}

FarkÄ±nÄ± dÃ¶ndÃ¼rdÃ¼kten sonra `tmp` Ã¶ÄŸesinin `counter` deÄŸerinden kÃ¼Ã§Ã¼k veya eÅŸit olduÄŸundan emin olmak istiyoruz. Bir Echidna Ã¶zelliÄŸi yazabiliriz, ancak `tmp` deÄŸerini bir yerde saklamamÄ±z gerekecek. Onun yerine, bunun gibi bir teyit kullanabilirdik:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    assert (tmp <= counter);
    return (counter - tmp);
  }
}
```

### Echidna'yÄ± Ã§alÄ±ÅŸtÄ±rÄ±n {#run-echidna-2}

Teyit hatasÄ± testini etkinleÅŸtirmek iÃ§in bir [Echidna yapÄ±landÄ±rma dosyasÄ±](https://github.com/crytic/echidna/wiki/Config) `config.yaml` oluÅŸturun:

```yaml
checkAsserts: true
```

Bu sÃ¶zleÅŸmeyi Echidna'da Ã§alÄ±ÅŸtÄ±rdÄ±ÄŸÄ±mÄ±zda, beklenen sonuÃ§larÄ± elde ediyoruz:

```bash
echidna-test assert.sol --config config.yaml
Analyzing contract: assert.sol:Incrementor
assertion in inc: failed!ğŸ’¥
  Call sequence, shrinking (2596/5000):
    inc(21711016731996786641919559689128982722488122124807605757398297001483711807488)
    inc(7237005577332262213973186563042994240829374041602535252466099000494570602496)
    inc(86844066927987146567678238756515930889952488499230423029593188005934847229952)

Seed: 1806480648350826486
```

GÃ¶rdÃ¼ÄŸÃ¼nÃ¼z gibi, Echidna `inc` fonksiyonunda bazÄ± onaylama hatalarÄ± bildiriyor. Fonksiyon baÅŸÄ±na birden fazla teyit eklemek mÃ¼mkÃ¼ndÃ¼r, ancak Echidna hangi iddianÄ±n baÅŸarÄ±sÄ±z olduÄŸunu sÃ¶yleyemez.

### Teyitler nerede ve nasÄ±l kullanÄ±lÄ±r {#when-and-how-use-assertions}

Teyitler, Ã¶zellikle kontrol edilecek koÅŸullar bazÄ± `f` iÅŸlemlerinin doÄŸru kullanÄ±mÄ±yla doÄŸrudan ilgiliyse, aÃ§Ä±k Ã¶zelliklere alternatif olarak kullanÄ±labilir. BazÄ± kodlardan sonra teyitler eklemek, kontrolÃ¼n yÃ¼rÃ¼tÃ¼ldÃ¼kten hemen sonra yapÄ±lmasÄ±nÄ± zorunlu kÄ±lar:

```solidity
function f(..) public {
    // some complex code
    ...
    assert (condition);
    ...
}

```

Aksine, aÃ§Ä±k bir echidna Ã¶zelliÄŸi kullanmak iÅŸlemleri rastgele yÃ¼rÃ¼tecektir ve tam olarak ne zaman kontrol edileceÄŸini zorlamanÄ±n kolay bir yolu yoktur. Bu geÃ§ici Ã§Ã¶zÃ¼mÃ¼ yapmak hÃ¢lÃ¢ mÃ¼mkÃ¼ndÃ¼r:

```solidity
function echidna_assert_after_f() public returns (bool) {
    f(..);
    return(condition);
}
```

Ancak, bazÄ± sorunlar vardÄ±r:

- `f` `internal` veya `external` olarak duyurulursa baÅŸarÄ±sÄ±z olur.
- `f`'u Ã§aÄŸÄ±rmak iÃ§in hangi baÄŸÄ±msÄ±z deÄŸiÅŸkenlerin kullanÄ±lmasÄ± gerektiÄŸi aÃ§Ä±k deÄŸil.
- `f` geri dÃ¶nerse, Ã¶zellik baÅŸarÄ±sÄ±z olur.

Genel olarak, teyitlerin nasÄ±l kullanÄ±lacaÄŸÄ±na iliÅŸkin [John Regehr'in tavsiyesini](https://blog.regehr.org/archives/1091) izlemenizi Ã¶neririz:

- Teyit kontrolÃ¼ sÄ±rasÄ±nda herhangi bir yan etkiyi zorlamayÄ±n. Ã–rnek olarak: `assert(ChangeStateAndReturn() == 1)`
- AÃ§Ä±k ifadeleri teyit etmeyin. Ã–rnek olarak `var`'Ä±n `uint` olarak duyurulduÄŸu yerde `assert(var >= 0)` olmasÄ± gibi.

Son olarak, Echidna bunu algÄ±lamayacaÄŸÄ± (ancak sÃ¶zleÅŸme yine de geri dÃ¶necek) iÃ§in lÃ¼tfen `assert` yerine `require` **kullanmayÄ±n**.

### Ã–zet: Teyit KontrolÃ¼ {#summary-assertion-checking}

AÅŸaÄŸÄ±dakiler, Ã¶rneÄŸimizde echidna'nÄ±n Ã§alÄ±ÅŸmasÄ±nÄ± Ã¶zetler:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    assert (tmp <= counter);
    return (counter - tmp);
  }
}
```

```bash
echidna-test assert.sol --config config.yaml
Analyzing contract: assert.sol:Incrementor
assertion in inc: failed!ğŸ’¥
  Call sequence, shrinking (2596/5000):
    inc(21711016731996786641919559689128982722488122124807605757398297001483711807488)
    inc(7237005577332262213973186563042994240829374041602535252466099000494570602496)
    inc(86844066927987146567678238756515930889952488499230423029593188005934847229952)

Seed: 1806480648350826486
```

Echidna, bu fonksiyon bÃ¼yÃ¼k argÃ¼manlarla birden Ã§ok kez Ã§aÄŸrÄ±lÄ±rsa `inc` iÃ§indeki teyidin baÅŸarÄ±sÄ±z olabileceÄŸini buldu.

## Bir Echidna korpusunu toplama ve deÄŸiÅŸtirme {#collecting-and-modifying-an-echidna-corpus}

Echidna ile bir iÅŸlem korpusunun nasÄ±l toplanÄ±p kullanÄ±lacaÄŸÄ±nÄ± gÃ¶receÄŸiz. Hedef, aÅŸaÄŸÄ±daki akÄ±llÄ± sÃ¶zleÅŸme [`magic.sol`'dur](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/magic.sol):

```solidity
contract C {
  bool value_found = false;
  function magic(uint magic_1, uint magic_2, uint magic_3, uint magic_4) public {
    require(magic_1 == 42);
    require(magic_2 == 129);
    require(magic_3 == magic_4+333);
    value_found = true;
    return;
  }

  function echidna_magic_values() public returns (bool) {
    return !value_found;
  }

}
```

Bu kÃ¼Ã§Ã¼k Ã¶rnek, Echidna'yÄ± bir durum deÄŸiÅŸkenini deÄŸiÅŸtirmek iÃ§in belirli deÄŸerleri bulmaya zorlar. Bu, bir bulandÄ±rÄ±cÄ± iÃ§in zordur ([Manticore](https://github.com/trailofbits/manticore) gibi sembolik bir yÃ¼rÃ¼tme aracÄ± kullanÄ±lmasÄ± Ã¶nerilir). Bunu doÄŸrulamak iÃ§in Echidna'yÄ± Ã§alÄ±ÅŸtÄ±rabiliriz:

```bash
echidna-test magic.sol
...

echidna_magic_values: passed! ğŸ‰

Seed: 2221503356319272685
```

Ancak, bu bulandÄ±rma Ã§alÄ±ÅŸmasÄ±nÄ± yÃ¼rÃ¼tÃ¼rken korpus toplamak iÃ§in hÃ¢lÃ¢ Echidna'yÄ± kullanabiliriz.

### Bir korpus toplama {#collecting-a-corpus}

Korpus toplamayÄ± etkinleÅŸtirmek iÃ§in bir korpus dizini oluÅŸturun:

```bash
mkdir corpus-magic
```

Bir [Echidna konfigÃ¼rasyon dosyasÄ±](https://github.com/crytic/echidna/wiki/Config) `config.yaml` da oluÅŸturun:

```yaml
coverage: true
corpusDir: "corpus-magic"
```

Åimdi aracÄ±mÄ±zÄ± Ã§alÄ±ÅŸtÄ±rabilir ve toplanan korpusu kontrol edebiliriz:

```bash
echidna-test magic.sol --config config.yaml
```

Echidna hÃ¢lÃ¢ doÄŸru sihirli deÄŸerleri bulamÄ±yor ancak topladÄ±ÄŸÄ± korpusa bakabiliriz. Ã–rneÄŸin bu dosyalardan biri ÅŸuydu:

```json
[
  {
    "_gas'": "0xffffffff",
    "_delay": ["0x13647", "0xccf6"],
    "_src": "00a329c0648769a73afac7f9381e08fb43dbea70",
    "_dst": "00a329c0648769a73afac7f9381e08fb43dbea72",
    "_value": "0x0",
    "_call": {
      "tag": "SolCall",
      "contents": [
        "magic",
        [
          {
            "contents": [
              256,
              "93723985220345906694500679277863898678726808528711107336895287282192244575836"
            ],
            "tag": "AbiUInt"
          },
          {
            "contents": [256, "334"],
            "tag": "AbiUInt"
          },
          {
            "contents": [
              256,
              "68093943901352437066264791224433559271778087297543421781073458233697135179558"
            ],
            "tag": "AbiUInt"
          },
          {
            "tag": "AbiUInt",
            "contents": [256, "332"]
          }
        ]
      ]
    },
    "_gasprice'": "0xa904461f1"
  }
]
```

Bu girdinin Ã¶zelliÄŸimizdeki baÅŸarÄ±sÄ±zlÄ±ÄŸÄ± tetiklemeyeceÄŸi aÃ§Ä±ktÄ±r. Ancak bir sonraki adÄ±mda bunun iÃ§in nasÄ±l yapÄ±landÄ±rÄ±labileceÄŸini gÃ¶receÄŸiz.

### Bir korpus tohumlama {#seeding-a-corpus}

Echidna'nÄ±n `magic` fonksiyonuyla baÅŸa Ã§Ä±kabilmesi iÃ§in biraz yardÄ±ma ihtiyacÄ± var. Bunun iÃ§in uygun parametreleri kullanmak iÃ§in girdiyi kopyalayÄ±p deÄŸiÅŸtireceÄŸiz:

```bash
cp corpus/2712688662897926208.txt corpus/new.txt
```

`magic(42,129,333,0)` Ã§aÄŸÄ±rmasÄ± iÃ§in `new.txt`'yi dÃ¼zenleyeceÄŸiz. Åimdi, Echidna'yÄ± yeniden Ã§alÄ±ÅŸtÄ±rabiliriz:

```bash
echidna-test magic.sol --config config.yaml
...
echidna_magic_values: failed!ğŸ’¥
  Call sequence:
    magic(42,129,333,0)


Unique instructions: 142
Unique codehashes: 1
Seed: -7293830866560616537

```

Bu kez, Ã¶zelliÄŸin ihlal edildiÄŸini hemen tespit etti.

## YÃ¼ksek gaz tÃ¼ketimi olan iÅŸlemleri bulma {#finding-transactions-with-high-gas-consumption}

Echidna ile yÃ¼ksek gaz tÃ¼ketimi olan iÅŸlemleri nasÄ±l bulacaÄŸÄ±mÄ±zÄ± gÃ¶receÄŸiz. Hedef, aÅŸaÄŸÄ±daki akÄ±llÄ± sÃ¶zleÅŸmedir:

```solidity
contract C {
  uint state;

  function expensive(uint8 times) internal {
    for(uint8 i=0; i < times; i++)
      state = state + i;
  }

  function f(uint x, uint y, uint8 times) public {
    if (x == 42 && y == 123)
      expensive(times);
    else
      state = 0;
  }

  function echidna_test() public returns (bool) {
    return true;
  }

}
```

Burada `expensive` bÃ¼yÃ¼k bir gaz tÃ¼ketimine sahip olabilir.

Åu anda Echidna'nÄ±n test etmek iÃ§in her zaman bir Ã¶zelliÄŸe ihtiyacÄ± vardÄ±r: burada `echidna_test` her zaman `true` deÄŸerini dÃ¶ndÃ¼rÃ¼r. Bunu doÄŸrulamak iÃ§in Echidna'yÄ± Ã§alÄ±ÅŸtÄ±rabiliriz:

```
echidna-test gas.sol
...
echidna_test: passed! ğŸ‰

Seed: 2320549945714142710
```

### Gaz TÃ¼ketimini Hesaplama {#measuring-gas-consumption}

Echidna ile gaz tÃ¼ketimini etkinleÅŸtirmek iÃ§in bir `config.yaml` yapÄ±landÄ±rma dosyasÄ± oluÅŸturun:

```yaml
estimateGas: true
```

Bu Ã¶rnekte, sonuÃ§larÄ±n anlaÅŸÄ±lmasÄ±nÄ± kolaylaÅŸtÄ±rmak iÃ§in iÅŸlem sÄ±rasÄ±nÄ±n boyutunu da azaltacaÄŸÄ±z:

```yaml
seqLen: 2
estimateGas: true
```

### Echidna'yÄ± Ã§alÄ±ÅŸtÄ±rÄ±n {#run-echidna-3}

YapÄ±landÄ±rma dosyasÄ±nÄ± oluÅŸturduktan sonra Echidna'yÄ± ÅŸu ÅŸekilde Ã§alÄ±ÅŸtÄ±rabiliriz:

```bash
echidna-test gas.sol --config config.yaml
...
echidna_test: passed! ğŸ‰

f used a maximum of 1333608 gas
  Call sequence:
    f(42,123,249) Gas price: 0x10d5733f0a Time delay: 0x495e5 Block delay: 0x88b2

Unique instructions: 157
Unique codehashes: 1
Seed: -325611019680165325

```

- GÃ¶sterilen gaz [HEVM](https://github.com/dapphub/dapptools/tree/master/src/hevm#hevm-) tarafÄ±ndan saÄŸlanan bir tahmindir.

### Gaz DÃ¼ÅŸÃ¼rÃ¼cÃ¼ Ã‡aÄŸrÄ±larÄ± Filtreleme {#filtering-out-gas-reducing-calls}

YukarÄ±daki **bir bulandÄ±rma Ã§alÄ±ÅŸmasÄ± sÄ±rasÄ±nda Ã§aÄŸrÄ±lacak fonksiyonlarÄ± filtreleme** hakkÄ±ndaki Ã¶ÄŸretici, bazÄ± fonksiyonlarÄ±n testinizden nasÄ±l kaldÄ±rÄ±lacaÄŸÄ±nÄ± gÃ¶sterir.  
Bu, doÄŸru bir gaz tahmini elde etmek iÃ§in kritik Ã¶neme sahiptir. AÅŸaÄŸÄ±daki Ã¶rneÄŸi gÃ¶z Ã¶nÃ¼nde bulundurun:

```solidity
contract C {
  address [] addrs;
  function push(address a) public {
    addrs.push(a);
  }
  function pop() public {
    addrs.pop();
  }
  function clear() public{
    addrs.length = 0;
  }
  function check() public{
    for(uint256 i = 0; i < addrs.length; i++)
      for(uint256 j = i+1; j < addrs.length; j++)
        if (addrs[i] == addrs[j])
          addrs[j] = address(0x0);
  }
  function echidna_test() public returns (bool) {
      return true;
  }
}
```

Echidna tÃ¼m fonksiyonlarÄ± Ã§aÄŸÄ±rabilirse yÃ¼ksek gaz maliyeti olan iÅŸlemleri kolayca bulamaz:

```
echidna-test pushpop.sol --config config.yaml
...
pop used a maximum of 10746 gas
...
check used a maximum of 23730 gas
...
clear used a maximum of 35916 gas
...
push used a maximum of 40839 gas
```

Bunun nedeni, maliyetin `addrs` boyutuna baÄŸlÄ± olmasÄ± ve rastgele aramalarÄ±n diziyi neredeyse boÅŸ bÄ±rakma eÄŸiliminde olmasÄ±dÄ±r. Ancak `pop` ve `clear`'Ä± kara listeye almak bize Ã§ok daha iyi sonuÃ§lar verir:

```yaml
filterBlacklist: true
filterFunctions: ["pop", "clear"]
```

```
echidna-test pushpop.sol --config config.yaml
...
push used a maximum of 40839 gas
...
check used a maximum of 1484472 gas
```

### Ã–zet: YÃ¼ksek gaz tÃ¼ketimi olan iÅŸlemleri bulma {#summary-finding-transactions-with-high-gas-consumption}

Echidna, `estimateGas` yapÄ±landÄ±rma seÃ§eneÄŸini kullanarak yÃ¼ksek gaz tÃ¼ketimi olan iÅŸlemleri bulabilir:

```yaml
estimateGas: true
```

```bash
echidna-test contract.sol --config config.yaml
...
```

Echidna, bulandÄ±rma Ã§alÄ±ÅŸmasÄ± sona erdiÄŸinde her fonksiyon iÃ§in maksimum gaz tÃ¼ketimine sahip bir dizi raporlayacaktÄ±r.
