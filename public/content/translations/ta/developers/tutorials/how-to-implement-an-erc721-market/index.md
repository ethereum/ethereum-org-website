---
title: ERC-721 சந்தையை எவ்வாறு செயல்படுத்துவது
description: விகேந்திர விளம்பரப் பலகையில் டோக்கனைஸ்டு பொருட்களை விற்பனைக்கு வைப்பது எப்படி
author: "Alberto Cuesta Cañada"
tags:
  [
    "ஸ்மார்ட் ஒப்பந்தங்கள்",
    "erc-721",
    "திட்பம்",
    "டோக்கன்கள்"
  ]
skill: intermediate
lang: ta
published: 2020-03-19
source: Hackernoon
sourceUrl: https://hackernoon.com/how-to-implement-an-erc721-market-1e1a32j9
---

இந்தக் கட்டுரையில், Ethereum blockchain-க்கு Craigslist-ஐ எவ்வாறு குறியீடு செய்வது என்பதைக் காட்டப் போகிறேன்.

Gumtree, Ebay மற்றும் Craigslist-க்கு முன்பு, விளம்பரப் பலகைகள் பெரும்பாலும் கார்க் அல்லது காகிதத்தால் செய்யப்பட்டிருந்தன. பள்ளி தாழ்வாரங்கள், செய்தித்தாள்கள், தெருவிளக்குகள், கடை முகப்புகளில் விளம்பரப் பலகைகள் இருந்தன.

இணையத்தின் வருகையால் அவை அனைத்தும் மாறின. ஒரு குறிப்பிட்ட விளம்பரப் பலகையைப் பார்க்கக்கூடியவர்களின் எண்ணிக்கை பல மடங்கு பெருகியது. அதனால், அவை பிரதிநிதித்துவப்படுத்தும் சந்தைகள் மிகவும் திறமையானவையாக மாறி உலகளாவிய அளவிற்கு விரிவடைந்தன. Ebay என்பது ஒரு பெரிய வணிகமாகும், அதன் தோற்றம் இந்த பௌதீக விளம்பரப் பலகைகளில் இருந்து தொடங்குகிறது.

Blockchain மூலம் இந்தச் சந்தைகள் மீண்டும் ஒருமுறை மாற உள்ளன, அதை நான் உங்களுக்குக் காட்டுகிறேன்.

## பணமாக்குதல் {#monetization}

ஒரு பொது blockchain விளம்பரப் பலகையின் வணிக மாதிரி Ebay மற்றும் பிற நிறுவனங்களின் மாதிரியிலிருந்து வேறுபட்டதாக இருக்க வேண்டும்.

முதலில், [விகேந்திரமாக்கல் கோணம்](/developers/docs/web2-vs-web3/) உள்ளது. தற்போதுள்ள தளங்கள் தங்களின் சொந்த சேவையகங்களைப் பராமரிக்க வேண்டும். ஒரு விகேந்திர தளம் அதன் பயனர்களால் பராமரிக்கப்படுகிறது, எனவே முக்கிய தளத்தை இயக்குவதற்கான செலவு தள உரிமையாளருக்கு பூஜ்ஜியமாகக் குறைகிறது.

பிறகு முன் முனை, அதாவது தளத்திற்கான அணுகலை வழங்கும் இணையதளம் அல்லது இடைமுகம் உள்ளது. இங்கே பல விருப்பங்கள் உள்ளன. தள உரிமையாளர்கள் அணுகலைக் கட்டுப்படுத்தி, அனைவரையும் தங்கள் இடைமுகத்தைப் பயன்படுத்த கட்டாயப்படுத்தி, ஒரு கட்டணத்தை வசூலிக்கலாம். தள உரிமையாளர்கள் அணுகலைத் திறக்கவும் முடிவு செய்யலாம் (மக்களுக்கு அதிகாரம்!) மற்றும் யாரை வேண்டுமானாலும் தளத்திற்கான இடைமுகங்களை உருவாக்க அனுமதிக்கலாம். அல்லது உரிமையாளர்கள் அந்த உச்சகட்டங்களுக்கு இடையில் எந்தவொரு அணுகுமுறையையும் முடிவு செய்யலாம்.

_என்னை விட அதிக தொலைநோக்குப் பார்வை கொண்ட வணிகத் தலைவர்களுக்கு இதை எப்படிப் பணமாக்குவது என்று தெரியும். நான் பார்ப்பது எல்லாம் இது தற்போதைய நிலையிலிருந்து வேறுபட்டது மற்றும் அநேகமாக லாபகரமானது._

மேலும், ஆட்டோமேஷன் மற்றும் கொடுப்பனவுகள் கோணமும் உள்ளது. சில விஷயங்களை மிக [திறம்பட டோக்கனைஸ் செய்து](https://hackernoon.com/tokenization-of-digital-assets-g0ffk3v8s?ref=hackernoon.com) ஒரு விளம்பரப் பலகையில் வர்த்தகம் செய்யலாம். டோக்கனைஸ்டு சொத்துக்களை ஒரு blockchain-இல் எளிதாக மாற்ற முடியும். மிகவும் சிக்கலான கட்டண முறைகளை ஒரு blockchain-இல் எளிதாக செயல்படுத்த முடியும்.

நான் இங்கே ஒரு வணிக வாய்ப்பை உணர்கிறேன். இயக்கச் செலவுகள் இல்லாத ஒரு விளம்பரப் பலகையை எளிதாகச் செயல்படுத்தலாம், ஒவ்வொரு பரிவர்த்தனையிலும் சிக்கலான கட்டணப் பாதைகள் சேர்க்கப்பட்டுள்ளன. இதை எதற்குப் பயன்படுத்துவது என்பது பற்றி யாராவது ஒரு யோசனையுடன் வருவார்கள் என்று நான் உறுதியாக நம்புகிறேன்.

நான் அதை உருவாக்குவதில் மகிழ்ச்சி அடைகிறேன். குறியீட்டைப் பார்ப்போம்.

## செயல்படுத்துதல் {#implementation}

சில காலத்திற்கு முன்பு நாங்கள் வணிக வழக்கு எடுத்துக்காட்டு செயலாக்கங்கள் மற்றும் பிற நல்ல விஷயங்களுடன் ஒரு [திறந்த மூல களஞ்சியத்தை](https://github.com/HQ20/contracts?ref=hackernoon.com) தொடங்கினோம், தயவுசெய்து பாருங்கள்.

இந்த [Ethereum விளம்பரப் பலகைக்கான](https://github.com/HQ20/contracts/tree/master/contracts/classifieds?ref=hackernoon.com) குறியீடு அங்கே உள்ளது, தயவுசெய்து அதைப் பயன்படுத்தவும், துஷ்பிரயோகம் செய்யவும். குறியீடு தணிக்கை செய்யப்படவில்லை என்பதை மட்டும் நினைவில் கொள்ளுங்கள், மேலும் பணத்தை அதில் செலுத்துவதற்கு முன் உங்கள் சொந்த விடாமுயற்சியை நீங்கள் செய்ய வேண்டும்.

பலகையின் அடிப்படைகள் சிக்கலானவை அல்ல. பலகையில் உள்ள அனைத்து விளம்பரங்களும் சில புலங்களைக் கொண்ட ஒரு struct ஆக இருக்கும்:

```solidity
struct Trade {
  address poster;
  uint256 item;
  uint256 price;
  bytes32 status; // Open, Executed, Cancelled
}
```

எனவே விளம்பரத்தை இடுகையிடும் ஒருவர் இருக்கிறார். விற்பனைக்கு ஒரு பொருள். பொருளுக்கான ஒரு விலை. வர்த்தகத்தின் நிலை திறந்த, செயல்படுத்தப்பட்ட அல்லது ரத்து செய்யப்பட்டதாக இருக்கலாம்.

இந்த வர்த்தகங்கள் அனைத்தும் ஒரு மேப்பிங்கில் வைக்கப்படும். ஏனெனில் Solidity-இல் உள்ள அனைத்தும் ஒரு மேப்பிங்காகத் தெரிகிறது. அது வசதியானது என்பதாலும் கூட.

```solidity
mapping(uint256 => Trade) public trades;
```

மேப்பிங்கைப் பயன்படுத்துவது என்பது ஒவ்வொரு விளம்பரத்தையும் இடுகையிடுவதற்கு முன்பு அதற்கான ஒரு ஐடியைக் கொண்டு வர வேண்டும், மேலும் ஒரு விளம்பரத்தில் செயல்படுவதற்கு முன்பு அதன் ஐடியை நாம் அறிந்திருக்க வேண்டும். ஸ்மார்ட் கான்ட்ராக்ட்டில் அல்லது முன்-முனையில் இதைக் கையாள பல வழிகள் உள்ளன. உங்களுக்கு சில சுட்டிகள் தேவைப்பட்டால், தயவுசெய்து கேட்கவும்.

அடுத்து நாம் கையாளும் அந்தப் பொருட்கள் என்ன, பரிவர்த்தனைக்கு பணம் செலுத்தப் பயன்படுத்தப்படும் இந்த நாணயம் என்ன என்ற கேள்வி வருகிறது.

பொருட்களைப் பொறுத்தவரை, அவை [ERC-721](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/IERC721.sol?ref=hackernoon.com) இடைமுகத்தை செயல்படுத்த வேண்டும் என்று நாங்கள் கேட்கப் போகிறோம், இது உண்மையில் ஒரு blockchain-இல் நிஜ உலகப் பொருட்களைப் பிரதிநிதித்துவப்படுத்துவதற்கான ஒரு வழியாகும், இருப்பினும் அது [டிஜிட்டல் சொத்துக்களுடன் சிறப்பாகச் செயல்படுகிறது](https://hackernoon.com/tokenization-of-digital-assets-g0ffk3v8s?ref=hackernoon.com). கன்ஸ்ட்ரக்டரில் எங்களின் சொந்த ERC721 ஒப்பந்தத்தைக் குறிப்பிடப் போகிறோம், அதாவது எங்கள் விளம்பரப் பலகையில் உள்ள எந்தவொரு சொத்துக்களும் முன்பே டோக்கனைஸ் செய்யப்பட்டிருக்க வேண்டும்.

கட்டணங்களுக்கு, நாங்கள் இதே போன்ற ஒன்றைச் செய்யப் போகிறோம். பெரும்பாலான blockchain திட்டங்கள் தங்களுக்கென சொந்த [ERC-20](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/ERC20.sol?ref=hackernoon.com) கிரிப்டோகரன்சியை வரையறுக்கின்றன. வேறு சிலர் DAI போன்ற ஒரு பிரதானமான ஒன்றைப் பயன்படுத்த விரும்புகிறார்கள். இந்த விளம்பரப் பலகையில், உங்கள் நாணயம் என்னவாக இருக்கும் என்பதை கட்டுமானத்தின் போது நீங்கள் முடிவு செய்ய வேண்டும். எளிதானது.

```solidity
constructor (
  address _currencyTokenAddress, address _itemTokenAddress
) public {
  currencyToken = IERC20(_currencyTokenAddress);
  itemToken = IERC721(_itemTokenAddress);
  tradeCounter = 0;
}
```

நாம் அந்த இடத்திற்கு வந்து கொண்டிருக்கிறோம். எங்களிடம் விளம்பரங்கள், வர்த்தகத்திற்கான பொருட்கள் மற்றும் கொடுப்பனவுகளுக்கான நாணயம் ஆகியவை உள்ளன. ஒரு விளம்பரத்தைச் செய்வது என்பது, உங்களிடம் அந்தப் பொருள் இருப்பதையும், அதை நீங்கள் இரண்டு முறை, ஒருவேளை வேறு பலகையில் இடுகையிடவில்லை என்பதையும் காட்ட, அந்தப் பொருளை எஸ்க்ரோவில் வைப்பதாகும்.

கீழேயுள்ள குறியீடு அதைத்தான் செய்கிறது. பொருளை எஸ்க்ரோவில் வைக்கிறது, விளம்பரத்தை உருவாக்குகிறது, சில வீட்டு பராமரிப்பு வேலைகளைச் செய்கிறது.

```solidity
function openTrade(uint256 _item, uint256 _price)
  public
{
  itemToken.transferFrom(msg.sender, address(this), _item);
  trades[tradeCounter] = Trade({
    poster: msg.sender,
    item: _item,
    price: _price,
    status: "Open"
  });
  tradeCounter += 1;
  emit TradeStatusChange(tradeCounter - 1, "Open");
}
```

வர்த்தகத்தை ஏற்றுக்கொள்வது என்பது ஒரு விளம்பரத்தைத் (வர்த்தகம்) தேர்ந்தெடுத்து, விலையைச் செலுத்தி, பொருளைப் பெறுவதாகும். கீழேயுள்ள குறியீடு ஒரு வர்த்தகத்தை மீட்டெடுக்கிறது. அது கிடைக்கிறதா என்று சரிபார்க்கிறது. பொருளுக்கு பணம் செலுத்துகிறது. பொருளை மீட்டெடுக்கிறது. விளம்பரத்தைப் புதுப்பிக்கிறது.

```solidity
function executeTrade(uint256 _trade)
  public
{
  Trade memory trade = trades[_trade];
  require(trade.status == "Open", "வர்த்தகம் திறக்கப்படவில்லை.");
  currencyToken.transferFrom(msg.sender, trade.poster, trade.price);
  itemToken.transferFrom(address(this), msg.sender, trade.item);
  trades[_trade].status = "Executed";
  emit TradeStatusChange(_trade, "Executed");
}
```

இறுதியாக, வாங்குபவர் ஏற்றுக்கொள்வதற்கு முன்பு விற்பனையாளர்கள் ஒரு வர்த்தகத்திலிருந்து பின்வாங்க ஒரு வழி உள்ளது. சில மாடல்களில், விளம்பரங்கள் காலாவதியாகும் முன் ஒரு குறிப்பிட்ட காலத்திற்கு நேரலையில் இருக்கும். உங்கள் சந்தையின் வடிவமைப்பைப் பொறுத்து, இது உங்கள் விருப்பம்.

குறியீடு ஒரு வர்த்தகத்தை செயல்படுத்தப் பயன்படுத்தப்பட்டதைப் போன்றது, நாணயம் கைகள் மாறவில்லை மற்றும் பொருள் விளம்பரத்தை இடுகையிட்டவருக்குத் திரும்பும் என்பதைத் தவிர.

```solidity
function cancelTrade(uint256 _trade)
  public
{
  Trade memory trade = trades[_trade];
  require(
    msg.sender == trade.poster,
    "வர்த்தகத்தை இடுகையிட்டவரால் மட்டுமே ரத்து செய்ய முடியும்."
  );
  require(trade.status == "Open", "வர்த்தகம் திறக்கப்படவில்லை.");
  itemToken.transferFrom(address(this), trade.poster, trade.item);
  trades[_trade].status = "Cancelled";
  emit TradeStatusChange(_trade, "Cancelled");
}
```

அவ்வளவுதான். செயல்படுத்தலின் இறுதிக்கு நீங்கள் வந்துவிட்டீர்கள். சில வணிகக் கருத்துக்கள் குறியீட்டில் வெளிப்படுத்தப்படும்போது எவ்வளவு சுருக்கமாக உள்ளன என்பது மிகவும் ஆச்சரியமாக இருக்கிறது, இதுவும் அந்த நிகழ்வுகளில் ஒன்றாகும். முழு ஒப்பந்தத்தையும் [எங்கள் ரெப்போவில்](https://github.com/HQ20/contracts/blob/master/contracts/classifieds/Classifieds.sol) பார்க்கவும்.

## முடிவுரை {#conclusion}

விளம்பரப் பலகைகள் ஒரு பொதுவான சந்தை உள்ளமைப்பாகும், இது இணையத்துடன் பெருமளவில் விரிவடைந்து, சில ஏகபோக வெற்றியாளர்களுடன் மிகவும் பிரபலமான வணிக மாதிரியாக மாறியது.

விளம்பரப் பலகைகள் ஒரு blockchain சூழலில் பிரதிபலிக்க எளிதான கருவியாகவும் இருக்கின்றன, இது தற்போதுள்ள ஜாம்பவான்களுக்கு ஒரு சவாலை சாத்தியமாக்கும் மிகவும் குறிப்பிட்ட அம்சங்களுடன் உள்ளது.

இந்தக் கட்டுரையில், ஒரு விளம்பரப் பலகை வணிகத்தின் வணிக யதார்த்தத்தை தொழில்நுட்ப செயலாக்கத்துடன் இணைக்க நான் ஒரு முயற்சி செய்தேன். உங்களிடம் சரியான திறன்கள் இருந்தால், ஒரு தொலைநோக்குப் பார்வையை உருவாக்கவும், செயல்படுத்துவதற்கான ஒரு வரைபடத்தை உருவாக்கவும் இந்த அறிவு உங்களுக்கு உதவ வேண்டும்.

எப்போதும் போலவே, நீங்கள் ஏதாவது வேடிக்கையானதை உருவாக்க விரும்பினால் மற்றும் சில ஆலோசனைகளை வரவேற்றால், தயவுசெய்து [என்னைத் தொடர்பு கொள்ளுங்கள்](https://albertocuesta.es/)! நான் உதவ எப்போதும் மகிழ்ச்சியடைகிறேன்.
