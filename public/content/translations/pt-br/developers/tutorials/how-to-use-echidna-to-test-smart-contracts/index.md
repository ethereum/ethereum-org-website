---
title: Como usar o Echidna para testar contratos inteligentes
description: Como usar o Echidna para testar automaticamente contratos inteligentes
author: "Trailofbits"
lang: pt-br
tags:
  [
    "solidez",
    "smart contracts",
    "seguran√ßa",
    "testando",
    "fuzzing"
  ]
skill: advanced
published: 2020-04-10
source: Building secure contracts
sourceUrl: https://github.com/crytic/building-secure-contracts/tree/master/program-analysis/echidna
---

## Instala√ß√£o {#installation}

O Echidna pode ser instalado atrav√©s do docker ou usando o bin√°rio pr√©-compilado.

### Echidna atrav√©s do docker {#echidna-through-docker}

```bash
docker pull trailofbits/eth-security-toolbox
docker run -it -v "$PWD":/home/training trailofbits/eth-security-toolbox
```

_O √∫ltimo comando executa o eth-security-toolbox em um docker que tem acesso ao seu diret√≥rio atual. Voc√™ pode alterar os arquivos do seu host e executar as ferramentas nos arquivos do docker_

Dentro do docker, execute:

```bash
solc-select 0.5.11
cd /home/training
```

### Bin√°rio {#binary}

[https://github.com/crytic/echidna/releases/tag/v1.4.0.0](https://github.com/crytic/echidna/releases/tag/v1.4.0.0)

## Introdu√ß√£o ao fuzzing baseado em propriedade {#introduction-to-property-based-fuzzing}

O Echidna √© um fuzzer baseado em propriedade, que descrevemos em nossas postagens de blog anteriores ([1](https://blog.trailofbits.com/2018/03/09/echidna-a-smart-fuzzer-for-ethereum/), [2](https://blog.trailofbits.com/2018/05/03/state-machine-testing-with-echidna/), [3](https://blog.trailofbits.com/2020/03/30/an-echidna-for-all-seasons/)).

### Fuzzing {#fuzzing}

[Fuzzing](https://wikipedia.org/wiki/Fuzzing) √© uma t√©cnica bem conhecida na comunidade de seguran√ßa. Consiste em gerar entradas que s√£o mais ou menos aleat√≥rias para encontrar bugs no programa. Fuzzers para software tradicional (como [AFL](http://lcamtuf.coredump.cx/afl/) ou [LibFuzzer](https://llvm.org/docs/LibFuzzer.html)) s√£o conhecidos por serem ferramentas eficientes para encontrar bugs.

Al√©m da gera√ß√£o puramente aleat√≥ria de entradas, existem muitas t√©cnicas e estrat√©gias para gerar boas entradas, incluindo:

- Obter feedback de cada execu√ß√£o e us√°-lo para guiar a gera√ß√£o. Por exemplo, se uma entrada rec√©m-gerada levar √† descoberta de um novo caminho, pode fazer sentido gerar novas entradas pr√≥ximas a ela.
- Gerar a entrada respeitando uma restri√ß√£o estrutural. Por exemplo, se sua entrada contiver um cabe√ßalho com um checksum, far√° sentido deixar que o fuzzer gere uma entrada que valide o checksum.
- Usar entradas conhecidas para gerar novas entradas: se voc√™ tiver acesso a um grande conjunto de dados de entrada v√°lida, seu fuzzer pode gerar novas entradas a partir deles, em vez de iniciar sua gera√ß√£o do zero. Normalmente, s√£o chamadas de _seeds_.

### Fuzzing baseado em propriedade {#property-based-fuzzing}

O Echidna pertence a uma fam√≠lia espec√≠fica de fuzzer: fuzzing baseado em propriedade, fortemente inspirado pelo [QuickCheck](https://wikipedia.org/wiki/QuickCheck). Em contraste com o fuzzer cl√°ssico, que tentar√° encontrar falhas, o Echidna tentar√° quebrar invariantes definidos pelo usu√°rio.

Em contratos inteligentes, as invariantes s√£o fun√ß√µes do Solidity que podem representar qualquer estado incorreto ou inv√°lido que o contrato possa alcan√ßar, incluindo:

- Controle de acesso incorreto: o invasor tornou-se o propriet√°rio do contrato.
- M√°quina de estado incorreta: os tokens podem ser transferidos enquanto o contrato est√° pausado.
- Aritm√©tica incorreta: o usu√°rio pode causar um underflow em seu saldo e obter tokens gratuitos ilimitados.

### Testando uma propriedade com o Echidna {#testing-a-property-with-echidna}

Veremos como testar um contrato inteligente com o Echidna. O alvo √© o seguinte contrato inteligente [`token.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/token.sol):

```solidity
contract Token{
  mapping(address => uint) public balances;
  function airdrop() public{
      balances[msg.sender] = 1000;
  }
  function consume() public{
      require(balances[msg.sender]>0);
      balances[msg.sender] -= 1;
  }
  function backdoor() public{
      balances[msg.sender] += 1;
  }
}
```

Vamos supor que este token deva ter as seguintes propriedades:

- Qualquer pessoa pode ter no m√°ximo 1.000 tokens
- O token n√£o pode ser transferido (n√£o √© um token ERC20)

### Escreva uma propriedade {#write-a-property}

As propriedades do Echidna s√£o fun√ß√µes do Solidity. Uma propriedade deve:

- N√£o ter argumentos
- Retornar `true` se for bem-sucedida
- Ter seu nome come√ßando com `echidna`

O Echidna ir√°:

- Gerar automaticamente transa√ß√µes arbitr√°rias para testar a propriedade.
- Relatar quaisquer transa√ß√µes que levem uma propriedade a retornar `false` ou a lan√ßar um erro.
- Descartar o efeito colateral ao chamar uma propriedade (ou seja, se a propriedade alterar uma vari√°vel de estado, ela ser√° descartada ap√≥s o teste)

A propriedade a seguir verifica se o chamador n√£o tem mais de 1.000 tokens:

```solidity
function echidna_balance_under_1000() public view returns(bool){
      return balances[msg.sender] <= 1000;
}
```

Use heran√ßa para separar seu contrato de suas propriedades:

```solidity
contract TestToken is Token{
      function echidna_balance_under_1000() public view returns(bool){
            return balances[msg.sender] <= 1000;
      }
  }
```

O [`token.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/token.sol) implementa a propriedade e herda do token.

### Iniciar um contrato {#initiate-a-contract}

O Echidna precisa de um [construtor](/developers/docs/smart-contracts/anatomy/#constructor-functions) sem argumentos. Se seu contrato precisar de uma inicializa√ß√£o espec√≠fica, voc√™ precisar√° faz√™-la no construtor.

Existem alguns endere√ßos espec√≠ficos no Echidna:

- `0x00a329c0648769A73afAc7F9381E08FB43dBEA72`, que chama o construtor.
- `0x10000`, `0x20000` e `0x00a329C0648769a73afAC7F9381e08fb43DBEA70`, que chamam aleatoriamente as outras fun√ß√µes.

N√£o precisamos de nenhuma inicializa√ß√£o espec√≠fica em nosso exemplo atual, como resultado, nosso construtor est√° vazio.

### Executar o Echidna {#run-echidna}

O Echidna √© iniciado com:

```bash
echidna-test contract.sol
```

Se contract.sol contiver v√°rios contratos, voc√™ pode especificar o alvo:

```bash
echidna-test contract.sol --contract MyContract
```

### Resumo: testando uma propriedade {#summary-testing-a-property}

O texto a seguir resume a execu√ß√£o do Echidna em nosso exemplo:

```solidity
contract TestToken is Token{
    constructor() public {}
        function echidna_balance_under_1000() public view returns(bool){
          return balances[msg.sender] <= 1000;
        }
  }
```

```bash
echidna-test testtoken.sol --contract TestToken
...

echidna_balance_under_1000: falhou!üí•
  Sequ√™ncia de chamadas, encolhendo (1205/5000):
    airdrop()
    backdoor()

...
```

O Echidna descobriu que a propriedade √© violada se `backdoor` for chamado.

## Filtrando fun√ß√µes para chamar durante uma campanha de fuzzing {#filtering-functions-to-call-during-a-fuzzing-campaign}

Veremos como filtrar as fun√ß√µes que sofrer√£o fuzzing.
O alvo √© o seguinte contrato inteligente:

```solidity
contract C {
  bool state1 = false;
  bool state2 = false;
  bool state3 = false;
  bool state4 = false;

  function f(uint x) public {
    require(x == 12);
    state1 = true;
  }

  function g(uint x) public {
    require(state1);
    require(x == 8);
    state2 = true;
  }

  function h(uint x) public {
    require(state2);
    require(x == 42);
    state3 = true;
  }

 function i() public {
    require(state3);
    state4 = true;
  }

  function reset1() public {
    state1 = false;
    state2 = false;
    state3 = false;
    return;
  }

  function reset2() public {
    state1 = false;
    state2 = false;
    state3 = false;
    return;
  }

  function echidna_state4() public returns (bool) {
    return (!state4);
  }
}
```

Este pequeno exemplo for√ßa o Echidna a encontrar uma determinada sequ√™ncia de transa√ß√µes para alterar uma vari√°vel de estado.
Isso √© dif√≠cil para um fuzzer (recomenda-se usar uma ferramenta de execu√ß√£o simb√≥lica como o [Manticore](https://github.com/trailofbits/manticore)).
Podemos executar o Echidna para verificar isso:

```bash
echidna-test multi.sol
...
echidna_state4: passou! üéâ
Seed: -3684648582249875403
```

### Fun√ß√µes de filtragem {#filtering-functions}

O Echidna tem dificuldade em encontrar a sequ√™ncia correta para testar este contrato porque as duas fun√ß√µes de redefini√ß√£o (`reset1` e `reset2`) definir√£o todas as vari√°veis de estado como `false`.
No entanto, podemos usar um recurso especial do Echidna para adicionar as fun√ß√µes de redefini√ß√£o √† lista de bloqueio ou para adicionar apenas as fun√ß√µes `f`, `g`,
`h` e `i` √† lista de permiss√µes.

Para adicionar fun√ß√µes √† lista de bloqueio, podemos usar este arquivo de configura√ß√£o:

```yaml
filterBlacklist: true
filterFunctions: ["reset1", "reset2"]
```

Outra abordagem para filtrar fun√ß√µes √© listar as fun√ß√µes permitidas. Para fazer isso, podemos usar este arquivo de configura√ß√£o:

```yaml
filterBlacklist: false
filterFunctions: ["f", "g", "h", "i"]
```

- `filterBlacklist` √© `true` por padr√£o.
- A filtragem ser√° realizada apenas por nome (sem par√¢metros). Se voc√™ tiver `f()` e `f(uint256)`, o filtro `"f"` corresponder√° a ambas as fun√ß√µes.

### Executar o Echidna {#run-echidna-1}

Para executar o Echidna com um arquivo de configura√ß√£o `blacklist.yaml`:

```bash
echidna-test multi.sol --config blacklist.yaml
...
echidna_state4: falhou!üí•
  Sequ√™ncia de chamadas:
    f(12)
    g(8)
    h(42)
    i()
```

O Echidna encontrar√° a sequ√™ncia de transa√ß√µes para falsear a propriedade quase imediatamente.

### Resumo: fun√ß√µes de filtragem {#summary-filtering-functions}

O Echidna pode adicionar fun√ß√µes a uma lista de bloqueio ou a uma lista de permiss√µes para chamar durante uma campanha de fuzzing usando:

```yaml
filterBlacklist: true
filterFunctions: ["f1", "f2", "f3"]
```

```bash
echidna-test contract.sol --config config.yaml
...
```

O Echidna inicia uma campanha de fuzzing adicionando `f1`, `f2` e `f3` √† lista de bloqueio ou chamando apenas essas fun√ß√µes, de acordo com o valor do booleano `filterBlacklist`.

## Como testar a asser√ß√£o do Solidity com o Echidna {#how-to-test-soliditys-assert-with-echidna}

Neste breve tutorial, vamos mostrar como usar o Echidna para testar a verifica√ß√£o de asser√ß√µes em contratos. Vamos supor que tenhamos um contrato como este:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    // tmp <= counter
    return (counter - tmp);
  }
}
```

### Escrever uma asser√ß√£o {#write-an-assertion}

Queremos ter certeza de que `tmp` √© menor ou igual a `counter` depois de retornar sua diferen√ßa. Poder√≠amos escrever uma
propriedade do Echidna, mas precisar√≠amos armazenar o valor `tmp` em algum lugar. Em vez disso, poder√≠amos usar uma asser√ß√£o como esta:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    assert (tmp <= counter);
    return (counter - tmp);
  }
}
```

### Executar o Echidna {#run-echidna-2}

Para habilitar o teste de falha de asser√ß√£o, crie um [arquivo de configura√ß√£o do Echidna](https://github.com/crytic/echidna/wiki/Config) `config.yaml`:

```yaml
checkAsserts: true
```

Quando executamos este contrato no Echidna, obtemos os resultados esperados:

```bash
echidna-test assert.sol --config config.yaml
Analyzing contract: assert.sol:Incrementor
assertion in inc: failed!üí•
  Call sequence, shrinking (2596/5000):
    inc(21711016731996786641919559689128982722488122124807605757398297001483711807488)
    inc(7237005577332262213973186563042994240829374041602535252466099000494570602496)
    inc(86844066927987146567678238756515930889952488499230423029593188005934847229952)

Seed: 1806480648350826486
```

Como voc√™ pode ver, o Echidna relata alguma falha de asser√ß√£o na fun√ß√£o `inc`. Adicionar mais de uma asser√ß√£o por fun√ß√£o √© poss√≠vel, mas o Echidna n√£o consegue dizer qual asser√ß√£o falhou.

### Quando e como usar asser√ß√µes {#when-and-how-use-assertions}

As asser√ß√µes podem ser usadas como alternativas a propriedades expl√≠citas, especialmente se as condi√ß√µes a serem verificadas estiverem diretamente relacionadas ao uso correto de alguma opera√ß√£o `f`. Adicionar asser√ß√µes ap√≥s algum c√≥digo garantir√° que a verifica√ß√£o ocorra imediatamente ap√≥s sua execu√ß√£o:

```solidity
function f(..) public {
    // some complex code
    ...
    assert (condition);
    ...
}

```

Pelo contr√°rio, o uso de uma propriedade expl√≠cita do Echidna executar√° transa√ß√µes aleatoriamente e n√£o h√° uma maneira f√°cil de for√ßar exatamente quando ela ser√° verificada. Ainda √© poss√≠vel fazer esta solu√ß√£o alternativa:

```solidity
function echidna_assert_after_f() public returns (bool) {
    f(..);
    return(condition);
}
```

No entanto, h√° alguns problemas:

- Falha se `f` for declarada como `internal` ou `external`.
- N√£o est√° claro quais argumentos devem ser usados para chamar `f`.
- Se `f` reverter, a propriedade falhar√°.

Em geral, recomendamos seguir a [recomenda√ß√£o de John Regehr](https://blog.regehr.org/archives/1091) sobre como usar asser√ß√µes:

- N√£o force nenhum efeito colateral durante a verifica√ß√£o de asser√ß√£o. Por exemplo: `assert(ChangeStateAndReturn() == 1)`
- N√£o fa√ßa asser√ß√µes √≥bvias. Por exemplo, `assert(var >= 0)` onde `var` √© declarado como `uint`.

Finalmente, **n√£o use** `require` em vez de `assert`, pois o Echidna n√£o ser√° capaz de detect√°-lo (mas o contrato ser√° revertido de qualquer maneira).

### Resumo: verifica√ß√£o de asser√ß√£o {#summary-assertion-checking}

O texto a seguir resume a execu√ß√£o do Echidna em nosso exemplo:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    assert (tmp <= counter);
    return (counter - tmp);
  }
}
```

```bash
echidna-test assert.sol --config config.yaml
Analyzing contract: assert.sol:Incrementor
assertion in inc: failed!üí•
  Call sequence, shrinking (2596/5000):
    inc(21711016731996786641919559689128982722488122124807605757398297001483711807488)
    inc(7237005577332262213973186563042994240829374041602535252466099000494570602496)
    inc(86844066927987146567678238756515930889952488499230423029593188005934847229952)

Seed: 1806480648350826486
```

O Echidna descobriu que a asser√ß√£o em `inc` pode falhar se essa fun√ß√£o for chamada v√°rias vezes com argumentos grandes.

## Coletando e modificando um corpus do Echidna {#collecting-and-modifying-an-echidna-corpus}

Veremos como coletar e usar um corpus de transa√ß√µes com o Echidna. O alvo √© o seguinte contrato inteligente [`magic.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/magic.sol):

```solidity
contract C {
  bool value_found = false;
  function magic(uint magic_1, uint magic_2, uint magic_3, uint magic_4) public {
    require(magic_1 == 42);
    require(magic_2 == 129);
    require(magic_3 == magic_4+333);
    value_found = true;
    return;
  }

  function echidna_magic_values() public returns (bool) {
    return !value_found;
  }

}
```

Este pequeno exemplo for√ßa o Echidna a encontrar determinados valores para alterar uma vari√°vel de estado. Isso √© dif√≠cil para um fuzzer
(recomenda-se usar uma ferramenta de execu√ß√£o simb√≥lica como o [Manticore](https://github.com/trailofbits/manticore)).
Podemos executar o Echidna para verificar isso:

```bash
echidna-test magic.sol
...

echidna_magic_values: passou! üéâ

Seed: 2221503356319272685
```

No entanto, ainda podemos usar o Echidna para coletar o corpus ao executar esta campanha de fuzzing.

### Coletando um corpus {#collecting-a-corpus}

Para habilitar a coleta de corpus, crie um diret√≥rio de corpus:

```bash
mkdir corpus-magic
```

E um [arquivo de configura√ß√£o do Echidna](https://github.com/crytic/echidna/wiki/Config) `config.yaml`:

```yaml
coverage: true
corpusDir: "corpus-magic"
```

Agora podemos executar nossa ferramenta e verificar o corpus coletado:

```bash
echidna-test magic.sol --config config.yaml
```

O Echidna ainda n√£o consegue encontrar os valores m√°gicos corretos, mas podemos dar uma olhada no corpus que ele coletou.
Por exemplo, um desses arquivos era:

```json
[
  {
    "_gas'": "0xffffffff",
    "_delay": ["0x13647", "0xccf6"],
    "_src": "00a329c0648769a73afac7f9381e08fb43dbea70",
    "_dst": "00a329c0648769a73afac7f9381e08fb43dbea72",
    "_value": "0x0",
    "_call": {
      "tag": "SolCall",
      "contents": [
        "magic",
        [
          {
            "contents": [
              256,
              "93723985220345906694500679277863898678726808528711107336895287282192244575836"
            ],
            "tag": "AbiUInt"
          },
          {
            "contents": [256, "334"],
            "tag": "AbiUInt"
          },
          {
            "contents": [
              256,
              "68093943901352437066264791224433559271778087297543421781073458233697135179558"
            ],
            "tag": "AbiUInt"
          },
          {
            "tag": "AbiUInt",
            "contents": [256, "332"]
          }
        ]
      ]
    },
    "_gasprice'": "0xa904461f1"
  }
]
```

Claramente, essa entrada n√£o acionar√° a falha em nossa propriedade. No entanto, na pr√≥xima etapa, veremos como modific√°-lo para isso.

### Semeando um corpus {#seeding-a-corpus}

O Echidna precisa de ajuda para lidar com a fun√ß√£o `magic`. Vamos copiar e modificar a entrada para usar par√¢metros
adequados para ela:

```bash
cp corpus/2712688662897926208.txt corpus/new.txt
```

Modificaremos o `new.txt` para chamar `magic(42,129,333,0)`. Agora, podemos executar novamente o Echidna:

```bash
echidna-test magic.sol --config config.yaml
...
echidna_magic_values: failed!üí•
  Call sequence:
    magic(42,129,333,0)


Unique instructions: 142
Unique codehashes: 1
Seed: -7293830866560616537

```

Desta vez, descobriu que a propriedade √© violada imediatamente.

## Encontrando transa√ß√µes com alto consumo de g√°s {#finding-transactions-with-high-gas-consumption}

Veremos como encontrar as transa√ß√µes com alto consumo de g√°s com o Echidna. O alvo √© o seguinte contrato inteligente:

```solidity
contract C {
  uint state;

  function expensive(uint8 times) internal {
    for(uint8 i=0; i < times; i++)
      state = state + i;
  }

  function f(uint x, uint y, uint8 times) public {
    if (x == 42 && y == 123)
      expensive(times);
    else
      state = 0;
  }

  function echidna_test() public returns (bool) {
    return true;
  }

}
```

Aqui, `expensive` pode ter um grande consumo de g√°s.

Atualmente, o Echidna sempre precisa de uma propriedade para testar: aqui, `echidna_test` sempre retorna `true`.
Podemos executar o Echidna para verificar isso:

```
echidna-test gas.sol
...
echidna_test: passou! üéâ

Seed: 2320549945714142710
```

### Medindo o consumo de g√°s {#measuring-gas-consumption}

Para habilitar o consumo de g√°s com o Echidna, crie um arquivo de configura√ß√£o `config.yaml`:

```yaml
estimateGas: true
```

Neste exemplo, tamb√©m reduziremos o tamanho da sequ√™ncia de transa√ß√µes para facilitar a compreens√£o dos resultados:

```yaml
seqLen: 2
estimateGas: true
```

### Executar o Echidna {#run-echidna-3}

Depois que o arquivo de configura√ß√£o for criado, podemos executar o Echidna desta forma:

```bash
echidna-test gas.sol --config config.yaml
...
echidna_test: passed! üéâ

f used a maximum of 1333608 gas
  Call sequence:
    f(42,123,249) Gas price: 0x10d5733f0a Time delay: 0x495e5 Block delay: 0x88b2

Unique instructions: 157
Unique codehashes: 1
Seed: -325611019680165325

```

- O g√°s mostrado √© uma estimativa fornecida pelo [HEVM](https://github.com/dapphub/dapptools/tree/master/src/hevm#hevm-).

### Filtrando chamadas que reduzem o g√°s {#filtering-out-gas-reducing-calls}

O tutorial sobre **como filtrar fun√ß√µes a serem chamadas durante uma campanha de fuzzing**, acima, mostra como
remover algumas fun√ß√µes do seu teste.  
Isso pode ser cr√≠tico para obter uma estimativa de g√°s precisa.
Considere o seguinte exemplo:

```solidity
contract C {
  address [] addrs;
  function push(address a) public {
    addrs.push(a);
  }
  function pop() public {
    addrs.pop();
  }
  function clear() public{
    addrs.length = 0;
  }
  function check() public{
    for(uint256 i = 0; i < addrs.length; i++)
      for(uint256 j = i+1; j < addrs.length; j++)
        if (addrs[i] == addrs[j])
          addrs[j] = address(0x0);
  }
  function echidna_test() public returns (bool) {
      return true;
  }
}
```

Se o Echidna puder chamar todas as fun√ß√µes, ele n√£o encontrar√° facilmente transa√ß√µes com alto custo de g√°s:

```
echidna-test pushpop.sol --config config.yaml
...
pop used a maximum of 10746 gas
...
check used a maximum of 23730 gas
...
clear used a maximum of 35916 gas
...
push used a maximum of 40839 gas
```

Isso porque o custo depende do tamanho de `addrs` e chamadas aleat√≥rias tendem a deixar o array quase vazio.
Isso ocorre porque o custo depende do tamanho de `addrs` e as chamadas aleat√≥rias tendem a deixar o array quase vazio. Adicionar `pop` e `clear` √† lista de bloqueio, no entanto, nos d√° resultados muito melhores:

```yaml
filterBlacklist: true
filterFunctions: ["pop", "clear"]
```

```
echidna-test pushpop.sol --config config.yaml
...
push used a maximum of 40839 gas
...
check used a maximum of 1484472 gas
```

### Resumo: encontrando transa√ß√µes com alto consumo de g√°s {#summary-finding-transactions-with-high-gas-consumption}

O Echidna pode encontrar transa√ß√µes com alto consumo de g√°s usando a op√ß√£o de configura√ß√£o `estimateGas`:

```yaml
estimateGas: true
```

```bash
echidna-test contract.sol --config config.yaml
...
```

O Echidna relatar√° uma sequ√™ncia com o consumo m√°ximo de g√°s para cada fun√ß√£o, uma vez que a campanha de fuzzing tenha terminado.
