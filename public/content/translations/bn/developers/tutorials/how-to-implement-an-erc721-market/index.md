---
title: কিভাবে একটি ERC-721 বাজার প্রয়োগ করবেন
description: কিভাবে একটি বিকেন্দ্রীভূত ক্লাসিফায়েড বোর্ডে টোকেনাইজড আইটেম বিক্রির জন্য রাখবেন
author: "আলবার্তো কুয়েস্তা কানাডা"
tags: [ "স্মার্ট কন্ট্র্যাক্ট", "erc-721", "সলিডিটি", "টোকেন" ]
skill: intermediate
lang: bn
published: 2020-03-19
source: Hackernoon
sourceUrl: https://hackernoon.com/how-to-implement-an-erc721-market-1e1a32j9
---

এই আর্টিকেলে, আমি আপনাকে দেখাতে যাচ্ছি কিভাবে Ethereum ব্লকচেইনের জন্য Craigslist কোড করতে হয়।

Gumtree, Ebay এবং Craigslist-এর আগে, ক্লাসিফায়েড বোর্ডগুলি বেশিরভাগই কর্ক বা কাগজ দিয়ে তৈরি ছিল। স্কুলের করিডোরে, সংবাদপত্রে, রাস্তার আলোতে, দোকানের সামনে ক্লাসিফায়েড বোর্ড ছিল।

ইন্টারনেটের সাথে সাথে এই সবকিছু বদলে গেছে। একটি নির্দিষ্ট ক্লাসিফায়েড বোর্ড দেখতে পারা মানুষের সংখ্যা বহুগুণে বেড়ে গেছে। এর সাথে, তারা যে বাজারগুলির প্রতিনিধিত্ব করে তা অনেক বেশি দক্ষ হয়ে ওঠে এবং বিশ্বব্যাপী আকারে স্কেল করা হয়েছিল। Ebay একটি বিশাল ব্যবসা যার উৎপত্তি এই শারীরিক ক্লাসিফায়েড বোর্ডগুলি থেকে হয়েছে।

ব্লকচেইনের সাথে এই বাজারগুলি আরও একবার পরিবর্তনের জন্য প্রস্তুত, আমি আপনাকে দেখাই কিভাবে।

## নগদীকরণ {#monetization}

একটি পাবলিক ব্লকচেইন ক্লাসিফায়েড বোর্ডের ব্যবসায়িক মডেল Ebay এবং কোম্পানির থেকে ভিন্ন হতে হবে।

প্রথমত, এখানে [বিকেন্দ্রীকরণের দৃষ্টিকোণ](/developers/docs/web2-vs-web3/) রয়েছে। বিদ্যমান প্ল্যাটফর্মগুলিকে তাদের নিজস্ব সার্ভার বজায় রাখতে হবে। একটি বিকেন্দ্রীভূত প্ল্যাটফর্ম তার ব্যবহারকারীদের দ্বারা রক্ষণাবেক্ষণ করা হয়, তাই প্ল্যাটফর্মের মালিকের জন্য মূল প্ল্যাটফর্মটি চালানোর খরচ শূন্যে নেমে আসে।

তারপর রয়েছে ফ্রন্ট এন্ড, ওয়েবসাইট বা ইন্টারফেস যা প্ল্যাটফর্মে অ্যাক্সেস দেয়। এখানে অনেক বিকল্প আছে। প্ল্যাটফর্মের মালিকরা অ্যাক্সেস সীমাবদ্ধ করতে পারে এবং প্রত্যেককে তাদের ইন্টারফেস ব্যবহার করতে বাধ্য করতে পারে, একটি ফি চার্জ করে। প্ল্যাটফর্মের মালিকরা অ্যাক্সেস খোলার সিদ্ধান্তও নিতে পারে (জনগণের হাতে ক্ষমতা!) এবং যে কাউকে প্ল্যাটফর্মে ইন্টারফেস তৈরি করতে দিন। অথবা মালিকরা সেই চরম পর্যায়গুলোর মাঝখানে যেকোনো পদ্ধতির সিদ্ধান্ত নিতে পারেন।

_আমার চেয়ে বেশি দূরদৃষ্টিসম্পন্ন ব্যবসায়িক নেতারা জানবেন কিভাবে এটিকে নগদীকরণ করা যায়। আমি শুধু দেখছি যে এটি স্থিতাবস্থা থেকে আলাদা এবং সম্ভবত লাভজনক।_

উপরন্তু, এখানে অটোমেশন এবং পেমেন্টের দৃষ্টিকোণ রয়েছে। কিছু জিনিস খুব [কার্যকরভাবে টোকেনাইজড](https://hackernoon.com/tokenization-of-digital-assets-g0ffk3v8s?ref=hackernoon.com) করা যেতে পারে এবং একটি ক্লাসিফায়েড বোর্ডে ট্রেড করা যেতে পারে। টোকেনাইজড অ্যাসেটগুলি একটি ব্লকচেইনে সহজেই স্থানান্তর করা যায়। অত্যন্ত জটিল পেমেন্ট পদ্ধতি একটি ব্লকচেইনে সহজেই প্রয়োগ করা যেতে পারে।

আমি এখানে শুধু একটি ব্যবসায়িক সুযোগের গন্ধ পাচ্ছি। কোনো চলমান খরচ ছাড়াই একটি ক্লাসিফায়েড বোর্ড সহজেই প্রয়োগ করা যেতে পারে, প্রতিটি লেনদেনে অন্তর্ভুক্ত জটিল পেমেন্ট পাথ সহ। আমি নিশ্চিত যে কেউ এটি কিসের জন্য ব্যবহার করতে হবে সে সম্পর্কে একটি ধারণা নিয়ে আসবে।

আমি শুধু এটি তৈরি করে খুশি। চলুন কোডটি একবার দেখে নেওয়া যাক।

## বাস্তবায়ন {#implementation}

কিছু সময় আগে আমরা ব্যবসায়িক ক্ষেত্রের উদাহরণ বাস্তবায়ন এবং অন্যান্য ভাল জিনিস সহ একটি [ওপেন সোর্স রিপোজিটরি](https://github.com/HQ20/contracts?ref=hackernoon.com) শুরু করেছি, অনুগ্রহ করে একবার দেখুন।

এই [Ethereum ক্লাসিফায়েড বোর্ড](https://github.com/HQ20/contracts/tree/master/contracts/classifieds?ref=hackernoon.com) এর কোডটি সেখানে আছে, দয়া করে এটি ব্যবহার করুন এবং যথেচ্ছভাবে ব্যবহার করুন। শুধু সচেতন থাকুন যে কোডটি অডিট করা হয়নি এবং এতে টাকা লাগানোর আগে আপনাকে নিজের যথাযথ সতর্কতা অবলম্বন করতে হবে।

বোর্ডের মূল বিষয়গুলি জটিল নয়। বোর্ডের সমস্ত বিজ্ঞাপনগুলি কয়েকটি ফিল্ড সহ একটি স্ট্রাকট হবে:

```solidity
struct Trade {
  address poster;
  uint256 item;
  uint256 price;
  bytes32 status; // খোলা, কার্যকর, বাতিল
}
```

সুতরাং বিজ্ঞাপন পোস্ট করার জন্য কেউ একজন আছেন। বিক্রির জন্য একটি আইটেম। আইটেমটির জন্য একটি মূল্য। ট্রেডের স্ট্যাটাস যা খোলা, কার্যকর বা বাতিল হতে পারে।

এই সমস্ত ট্রেড একটি ম্যাপিংয়ে রাখা হবে। কারণ Solidity-তে সবকিছুই একটি ম্যাপিং বলে মনে হয়। এছাড়াও কারণ এটি সুবিধাজনক।

```solidity
mapping(uint256 => Trade) public trades;
```

একটি ম্যাপিং ব্যবহার করার অর্থ হল, এটি পোস্ট করার আগে আমাদের প্রতিটি বিজ্ঞাপনের জন্য একটি আইডি তৈরি করতে হবে এবং এটিতে কাজ করার আগে আমাদের একটি বিজ্ঞাপনের আইডি জানতে হবে। স্মার্ট কন্ট্র্যাক্ট বা ফ্রন্ট-এন্ডে এর সাথে মোকাবিলা করার একাধিক উপায় রয়েছে। আপনার যদি কিছু দিকনির্দেশনার প্রয়োজন হয় তবে জিজ্ঞাসা করুন।

এরপরে প্রশ্ন আসে যে আমরা যে আইটেমগুলি নিয়ে কাজ করি সেগুলি কী, এবং লেনদেনের জন্য অর্থ প্রদানের জন্য ব্যবহৃত এই মুদ্রাটি কী।

আইটেমগুলির জন্য, আমরা কেবল জিজ্ঞাসা করতে যাচ্ছি যে তারা [ERC-721](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/IERC721.sol?ref=hackernoon.com) ইন্টারফেসটি প্রয়োগ করে, যা প্রকৃতপক্ষে ব্লকচেইনে বাস্তব বিশ্বের আইটেমগুলিকে উপস্থাপন করার একটি উপায়, যদিও এটি [ডিজিটাল অ্যাসেটগুলির সাথে সবচেয়ে ভাল কাজ করে](https://hackernoon.com/tokenization-of-digital-assets-g0ffk3v8s?ref=hackernoon.com)। আমরা কনস্ট্রাক্টরে আমাদের নিজস্ব ERC721 কন্ট্র্যাক্ট নির্দিষ্ট করতে যাচ্ছি, যার অর্থ আমাদের ক্লাসিফায়েড বোর্ডের যেকোনো অ্যাসেটকে আগে থেকে টোকেনাইজড করতে হবে।

পেমেন্টের জন্য, আমরা একই ধরনের কিছু করতে যাচ্ছি। বেশিরভাগ ব্লকচেইন প্রকল্প তাদের নিজস্ব [ERC-20](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/ERC20.sol?ref=hackernoon.com) ক্রিপটোকারেন্সি সংজ্ঞায়িত করে। অন্য কেউ কেউ DAI-এর মতো একটি মূলধারার মুদ্রা ব্যবহার করতে পছন্দ করেন। এই ক্লাসিফায়েড বোর্ডে, আপনার মুদ্রা কী হবে তা নির্মাণের সময় আপনাকে সিদ্ধান্ত নিতে হবে। সহজ।

```solidity
constructor (
  address _currencyTokenAddress, address _itemTokenAddress
) public {
  currencyToken = IERC20(_currencyTokenAddress);
  itemToken = IERC721(_itemTokenAddress);
  tradeCounter = 0;
}
```

আমরা প্রায় সেখানে পৌঁছে গেছি। আমরা বিজ্ঞাপন, ট্রেডের জন্য আইটেম এবং পেমেন্টের জন্য একটি মুদ্রা পেয়েছি। একটি বিজ্ঞাপন তৈরি করার অর্থ হল একটি আইটেম এসক্রোতে রাখা, এটি দেখানোর জন্য যে এটি আপনার কাছে আছে এবং আপনি এটি দুবার পোস্ট করেননি, সম্ভবত একটি ভিন্ন বোর্ডে।

নীচের কোডটি ঠিক তাই করে। আইটেমটি এসক্রোতে রাখে, বিজ্ঞাপন তৈরি করে, কিছু হাউসকিপিং করে।

```solidity
function openTrade(uint256 _item, uint256 _price)
  public
{
  itemToken.transferFrom(msg.sender, address(this), _item);
  trades[tradeCounter] = Trade({
    poster: msg.sender,
    item: _item,
    price: _price,
    status: "Open"
  });
  tradeCounter += 1;
  emit TradeStatusChange(tradeCounter - 1, "Open");
}
```

ট্রেডটি গ্রহণ করার অর্থ হল একটি বিজ্ঞাপন (ট্রেড) বেছে নেওয়া, মূল্য পরিশোধ করা, আইটেমটি গ্রহণ করা। নীচের কোডটি একটি ট্রেড পুনরুদ্ধার করে। এটি উপলব্ধ কিনা তা পরীক্ষা করে। আইটেমটির জন্য অর্থ প্রদান করে। আইটেমটি পুনরুদ্ধার করে। বিজ্ঞাপন আপডেট করে।

```solidity
function executeTrade(uint256 _trade)
  public
{
  Trade memory trade = trades[_trade];
  require(trade.status == "Open", "ট্রেডটি খোলা নেই।");
  currencyToken.transferFrom(msg.sender, trade.poster, trade.price);
  itemToken.transferFrom(address(this), msg.sender, trade.item);
  trades[_trade].status = "Executed";
  emit TradeStatusChange(_trade, "Executed");
}
```

অবশেষে, আমাদের কাছে বিক্রেতাদের জন্য একটি বিকল্প রয়েছে ক্রেতা গ্রহণ করার আগে একটি ট্রেড থেকে সরে আসার। কিছু মডেলে, বিজ্ঞাপনগুলি মেয়াদ শেষ হওয়ার আগে কিছু সময়ের জন্য লাইভ থাকে। আপনার বাজারের ডিজাইনের উপর নির্ভর করে এটি আপনার পছন্দ।

কোডটি একটি ট্রেড কার্যকর করার জন্য ব্যবহৃত কোডের সাথে খুব সাদৃশ্যপূর্ণ, শুধুমাত্র এখানে কোনো মুদ্রা হাতবদল হয় না এবং আইটেমটি বিজ্ঞাপন পোস্টারের কাছে ফিরে যায়।

```solidity
function cancelTrade(uint256 _trade)
  public
{
  Trade memory trade = trades[_trade];
  require(
    msg.sender == trade.poster,
    "ট্রেড শুধুমাত্র পোস্টার দ্বারা বাতিল করা যেতে পারে।"
  );
  require(trade.status == "Open", "ট্রেডটি খোলা নেই।");
  itemToken.transferFrom(address(this), trade.poster, trade.item);
  trades[_trade].status = "Cancelled";
  emit TradeStatusChange(_trade, "Cancelled");
}
```

এই পর্যন্তই। আপনি বাস্তবায়নের শেষ পর্যন্ত পৌঁছেছেন। কোডে প্রকাশ করা হলে কিছু ব্যবসায়িক ধারণা কতটা সংক্ষিপ্ত হয় তা বেশ আশ্চর্যজনক, এবং এটি সেইসব ক্ষেত্রের মধ্যে একটি। সম্পূর্ণ কন্ট্র্যাক্টটি [আমাদের রেপোতে](https://github.com/HQ20/contracts/blob/master/contracts/classifieds/Classifieds.sol) দেখুন।

## উপসংহার {#conclusion}

ক্লাসিফায়েড বোর্ডগুলি একটি সাধারণ বাজার কনফিগারেশন যা ইন্টারনেটের সাথে ব্যাপকভাবে প্রসারিত হয়েছে, এবং কিছু একচেটিয়া বিজয়ীর সাথে এটি একটি অত্যন্ত জনপ্রিয় ব্যবসায়িক মডেল হয়ে উঠেছে।

ক্লাসিফায়েড বোর্ডগুলি ব্লকচেইন পরিবেশে নকল করার জন্য একটি সহজ টুল, যার কিছু নির্দিষ্ট বৈশিষ্ট্য রয়েছে যা বিদ্যমান জায়ান্টদের জন্য একটি চ্যালেঞ্জ তৈরি করা সম্ভব করবে।

এই আর্টিকেলে, আমি একটি ক্লাসিফায়েড বোর্ড ব্যবসার ব্যবসায়িক বাস্তবতা এবং প্রযুক্তিগত বাস্তবায়নের মধ্যে একটি সেতুবন্ধন করার চেষ্টা করেছি। আপনার যদি সঠিক দক্ষতা থাকে তবে এই জ্ঞান আপনাকে বাস্তবায়নের জন্য একটি দৃষ্টিভঙ্গি এবং একটি রোডম্যাপ তৈরি করতে সাহায্য করবে।

বরাবরের মতো, আপনি যদি মজাদার কিছু তৈরি করতে চান এবং কিছু পরামর্শ চান, তাহলে অনুগ্রহ করে [আমার সাথে যোগাযোগ করুন](https://albertocuesta.es/)! আমি সাহায্য করতে সবসময় খুশি।
