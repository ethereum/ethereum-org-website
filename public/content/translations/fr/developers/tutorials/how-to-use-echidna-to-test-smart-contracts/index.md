---
title: Comment utiliser Echidna pour tester les contrats intelligents
description: Comment utiliser Echidna pour tester automatiquement les contrats intelligents
author: "Trailofbits"
lang: fr
tags:
  [
    "solidit√©",
    "contrats intelligents",
    "s√©curit√©",
    "test",
    "fuzzing"
  ]
skill: advanced
published: 2020-04-10
source: Building secure contracts
sourceUrl: https://github.com/crytic/building-secure-contracts/tree/master/program-analysis/echidna
---

## Installation {#installation}

Echidna peut √™tre install√© via Docker ou en utilisant un binaire pr√©compil√©.

### Echidna via Docker {#echidna-through-docker}

```bash
docker pull trailofbits/eth-security-toolbox
docker run -it -v "$PWD":/home/training trailofbits/eth-security-toolbox
```

_La derni√®re commande ex√©cute eth-security-toolbox dans un docker qui a acc√®s √† votre r√©pertoire actuel. Vous pouvez modifier les fichiers depuis votre h√¥te, et ex√©cuter les outils sur les fichiers depuis le docker_

Dans Docker, ex√©cutez :

```bash
solc-select 0.5.11
cd /home/training
```

### Binaire {#binary}

[https://github.com/crytic/echidna/releases/tag/v1.4.0.0](https://github.com/crytic/echidna/releases/tag/v1.4.0.0)

## Introduction au fuzzing bas√© sur les propri√©t√©s {#introduction-to-property-based-fuzzing}

Echidna est un fuzzer bas√© sur les propri√©t√©s, que nous avons d√©crit dans nos pr√©c√©dents articles de blog ([1](https://blog.trailofbits.com/2018/03/09/echidna-a-smart-fuzzer-for-ethereum/), [2](https://blog.trailofbits.com/2018/05/03/state-machine-testing-with-echidna/), [3](https://blog.trailofbits.com/2020/03/30/an-echidna-for-all-seasons/)).

### Fuzzing {#fuzzing}

[Le fuzzing](https://wikipedia.org/wiki/Fuzzing) est une technique bien connue dans la communaut√© de la s√©curit√©. Elle consiste √† g√©n√©rer des entr√©es plus ou moins al√©atoires pour trouver des bogues dans le programme. Les fuzzers pour les logiciels traditionnels (tels que [AFL](http://lcamtuf.coredump.cx/afl/) ou [LibFuzzer](https://llvm.org/docs/LibFuzzer.html)) sont connus pour √™tre des outils efficaces pour trouver des bogues.

Au-del√† de la g√©n√©ration purement al√©atoire d'entr√©es, il existe de nombreuses techniques et strat√©gies pour g√©n√©rer de bonnes entr√©es, notamment :

- Obtenir des retours de chaque ex√©cution et les utiliser pour guider la g√©n√©ration. Par exemple, si une nouvelle entr√©e g√©n√©r√©e m√®ne √† la d√©couverte d'un nouveau chemin, il peut √™tre judicieux de g√©n√©rer de nouvelles entr√©es proches de celle-ci.
- G√©n√©rer l'entr√©e en respectant une contrainte structurelle. Par exemple, si votre entr√©e contient un en-t√™te avec une somme de contr√¥le, il sera logique de laisser le fuzzer g√©n√©rer une entr√©e validant la somme de contr√¥le.
- Utiliser des entr√©es connues pour en g√©n√©rer de nouvelles : si vous avez acc√®s √† un grand jeu de donn√©es d'entr√©es valides, votre fuzzer peut g√©n√©rer de nouvelles entr√©es √† partir de celles-ci, plut√¥t que de commencer la g√©n√©ration √† partir de z√©ro. On les appelle g√©n√©ralement des _seeds_.

### Fuzzing bas√© sur les propri√©t√©s {#property-based-fuzzing}

Echidna appartient √† une famille sp√©cifique de fuzzer : le fuzzing bas√© sur les propri√©t√©s, fortement inspir√© de [QuickCheck](https://wikipedia.org/wiki/QuickCheck). Contrairement √† un fuzzer classique qui essaiera de trouver des plantages, Echidna essaiera de rompre des invariants d√©finis par l'utilisateur.

Dans les contrats intelligents, les invariants sont des fonctions Solidity, qui peuvent repr√©senter n'importe quel √©tat incorrect ou invalide que le contrat peut atteindre, notamment :

- Contr√¥le d'acc√®s incorrect : l'attaquant est devenu le propri√©taire du contrat.
- Machine d'√©tat incorrecte : les jetons peuvent √™tre transf√©r√©s alors que le contrat est en pause.
- Arithm√©tique incorrecte : l'utilisateur peut provoquer un underflow de son solde et obtenir un nombre illimit√© de jetons gratuits.

### Tester une propri√©t√© avec Echidna {#testing-a-property-with-echidna}

Nous verrons comment tester un contrat intelligent avec Echidna. La cible est le contrat intelligent suivant [`token.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/token.sol) :

```solidity
contract Token{
  mapping(address => uint) public balances;
  function airdrop() public{
      balances[msg.sender] = 1000;
  }
  function consume() public{
      require(balances[msg.sender]>0);
      balances[msg.sender] -= 1;
  }
  function backdoor() public{
      balances[msg.sender] += 1;
  }
}
```

Nous supposerons que ce jeton doit avoir les propri√©t√©s suivantes :

- N'importe qui peut avoir au maximum 1000 jetons
- Le jeton ne peut pas √™tre transf√©r√© (ce n'est pas un jeton ERC20)

### √âcrire une propri√©t√© {#write-a-property}

Les propri√©t√©s Echidna sont des fonctions Solidity. Une propri√©t√© doit :

- Ne pas avoir d'argument
- Renvoyer `true` si elle r√©ussit
- Avoir un nom qui commence par `echidna`

Echidna :

- G√©n√©rer automatiquement des transactions arbitraires pour tester la propri√©t√©.
- Signaler toute transaction qui am√®ne une propri√©t√© √† renvoyer `false` ou √† lever une erreur.
- Ignorer les effets de bord lors de l'appel d'une propri√©t√© (c.-√†-d. que si la propri√©t√© modifie une variable d'√©tat, la modification est annul√©e apr√®s le test)

La propri√©t√© suivante v√©rifie que l'appelant n'a pas plus de 1000 jetons :

```solidity
function echidna_balance_under_1000() public view returns(bool){
      return balances[msg.sender] <= 1000;
}
```

Utilisez l'h√©ritage pour s√©parer votre contrat de vos propri√©t√©s :

```solidity
contract TestToken is Token{
      function echidna_balance_under_1000() public view returns(bool){
            return balances[msg.sender] <= 1000;
      }
  }
```

[`token.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/token.sol) met en ≈ìuvre la propri√©t√© et h√©rite du jeton.

### Initialiser un contrat {#initiate-a-contract}

Echidna a besoin d'un [constructeur](/developers/docs/smart-contracts/anatomy/#constructor-functions) sans argument. Si votre contrat n√©cessite une initialisation sp√©cifique, vous devez la faire dans le constructeur.

Il y a quelques adresses sp√©cifiques dans Echidna :

- `0x00a329c0648769A73afAc7F9381E08FB43dBEA72` qui appelle le constructeur.
- `0x10000`, `0x20000` et `0x00a329C0648769a73afAC7F9381e08fb43DBEA70` qui appellent al√©atoirement les autres fonctions.

Nous n'avons pas besoin d'initialisation particuli√®re dans notre exemple actuel, par cons√©quent notre constructeur est vide.

### Ex√©cuter Echidna {#run-echidna}

Echidna se lance avec :

```bash
echidna-test contract.sol
```

Si contract.sol contient plusieurs contrats, vous pouvez sp√©cifier la cible :

```bash
echidna-test contract.sol --contract MyContract
```

### R√©sum√© : Tester une propri√©t√© {#summary-testing-a-property}

Ce qui suit r√©sume l'ex√©cution d'Echidna sur notre exemple :

```solidity
contract TestToken is Token{
    constructor() public {}
        function echidna_balance_under_1000() public view returns(bool){
          return balances[msg.sender] <= 1000;
        }
  }
```

```bash
echidna-test testtoken.sol --contract TestToken
...

echidna_balance_under_1000: failed!üí•
  Call sequence, shrinking (1205/5000):
    airdrop()
    backdoor()

...
```

Echidna a trouv√© que la propri√©t√© est viol√©e si `backdoor` est appel√©e.

## Filtrer les fonctions √† appeler pendant une campagne de fuzzing {#filtering-functions-to-call-during-a-fuzzing-campaign}

Nous verrons comment filtrer les fonctions √† fuzzer.
La cible est le contrat intelligent suivant :

```solidity
contract C {
  bool state1 = false;
  bool state2 = false;
  bool state3 = false;
  bool state4 = false;

  function f(uint x) public {
    require(x == 12);
    state1 = true;
  }

  function g(uint x) public {
    require(state1);
    require(x == 8);
    state2 = true;
  }

  function h(uint x) public {
    require(state2);
    require(x == 42);
    state3 = true;
  }

 function i() public {
    require(state3);
    state4 = true;
  }

  function reset1() public {
    state1 = false;
    state2 = false;
    state3 = false;
    return;
  }

  function reset2() public {
    state1 = false;
    state2 = false;
    state3 = false;
    return;
  }

  function echidna_state4() public returns (bool) {
    return (!state4);
  }
}
```

Ce petit exemple force Echidna √† trouver une certaine s√©quence de transactions pour modifier une variable d'√©tat.
C'est difficile pour un fuzzer (il est recommand√© d'utiliser un outil d'ex√©cution symbolique comme [Manticore](https://github.com/trailofbits/manticore)).
Nous pouvons ex√©cuter Echidna pour v√©rifier cela :

```bash
echidna-test multi.sol
...
echidna_state4: passed! üéâ
Seed: -3684648582249875403
```

### Filtrage des fonctions {#filtering-functions}

Echidna a du mal √† trouver la s√©quence correcte pour tester ce contrat, car les deux fonctions de r√©initialisation (`reset1` et `reset2`) mettront toutes les variables d'√©tat √† `false`.
Cependant, nous pouvons utiliser une fonctionnalit√© sp√©ciale d'Echidna pour soit mettre sur liste noire la fonction de r√©initialisation, soit mettre sur liste blanche uniquement les fonctions `f`, `g`,
`h` et `i`.

Pour mettre des fonctions sur liste noire, nous pouvons utiliser ce fichier de configuration :

```yaml
filterBlacklist: true
filterFunctions: ["reset1", "reset2"]
```

Une autre approche pour filtrer les fonctions consiste √† lister les fonctions sur liste blanche. Pour ce faire, nous pouvons utiliser ce fichier de configuration :

```yaml
filterBlacklist: false
filterFunctions: ["f", "g", "h", "i"]
```

- `filterBlacklist` est `true` par d√©faut.
- Le filtrage sera effectu√© uniquement par nom (sans les param√®tres). Si vous avez `f()` et `f(uint256)`, le filtre `"f"` correspondra aux deux fonctions.

### Ex√©cuter Echidna {#run-echidna-1}

Pour ex√©cuter Echidna avec un fichier de configuration `blacklist.yaml` :

```bash
echidna-test multi.sol --config blacklist.yaml
...
echidna_state4: failed!üí•
  Call sequence:
    f(12)
    g(8)
    h(42)
    i()
```

Echidna trouvera la s√©quence de transactions pour falsifier la propri√©t√© presque imm√©diatement.

### R√©sum√© : Filtrage des fonctions {#summary-filtering-functions}

Echidna peut soit mettre sur liste noire, soit mettre sur liste blanche les fonctions √† appeler pendant une campagne de fuzzing en utilisant :

```yaml
filterBlacklist: true
filterFunctions: ["f1", "f2", "f3"]
```

```bash
echidna-test contract.sol --config config.yaml
...
```

Echidna lance une campagne de fuzzing soit en mettant sur liste noire `f1`, `f2` et `f3`, soit en appelant uniquement celles-ci, en fonction de la valeur du bool√©en `filterBlacklist`.

## Comment tester l'assertion de Solidity avec Echidna {#how-to-test-soliditys-assert-with-echidna}

Dans ce court tutoriel, nous allons montrer comment utiliser Echidna pour tester la v√©rification d'assertions dans les contrats. Supposons que nous ayons un contrat comme celui-ci :

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    // tmp <= counter
    return (counter - tmp);
  }
}
```

### √âcrire une assertion {#write-an-assertion}

Nous voulons nous assurer que `tmp` est inf√©rieur ou √©gal √† `counter` apr√®s avoir renvoy√© leur diff√©rence. Nous pourrions √©crire une propri√©t√©
Echidna, mais nous devrons stocker la valeur de `tmp` quelque part. √Ä la place, nous pourrions utiliser une assertion comme celle-ci :

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    assert (tmp <= counter);
    return (counter - tmp);
  }
}
```

### Ex√©cuter Echidna {#run-echidna-2}

Pour activer le test d'√©chec d'assertion, cr√©ez un [fichier de configuration Echidna](https://github.com/crytic/echidna/wiki/Config) `config.yaml` :

```yaml
checkAsserts: true
```

Lorsque nous ex√©cutons ce contrat dans Echidna, nous obtenons les r√©sultats attendus :

```bash
echidna-test assert.sol --config config.yaml
Analyzing contract: assert.sol:Incrementor
assertion in inc: failed!üí•
  Call sequence, shrinking (2596/5000):
    inc(21711016731996786641919559689128982722488122124807605757398297001483711807488)
    inc(7237005577332262213973186563042994240829374041602535252466099000494570602496)
    inc(86844066927987146567678238756515930889952488499230423029593188005934847229952)

Seed: 1806480648350826486
```

Comme vous pouvez le voir, Echidna signale un √©chec d'assertion dans la fonction `inc`. Il est possible d'ajouter plus d'une assertion par fonction, mais Echidna ne peut pas dire quelle assertion a √©chou√©.

### Quand et comment utiliser les assertions {#when-and-how-use-assertions}

Les assertions peuvent √™tre utilis√©es comme alternatives aux propri√©t√©s explicites, surtout si les conditions √† v√©rifier sont directement li√©es √† l'utilisation correcte d'une op√©ration `f`. L'ajout d'assertions apr√®s un morceau de code garantira que la v√©rification aura lieu imm√©diatement apr√®s son ex√©cution :

```solidity
function f(..) public {
    // du code complexe
    ...
    assert (condition);
    ...
}

```

Au contraire, l'utilisation d'une propri√©t√© Echidna explicite ex√©cutera des transactions de mani√®re al√©atoire et il n'y a aucun moyen simple de forcer le moment exact o√π elle sera v√©rifi√©e. Il est toujours possible d'utiliser cette solution de contournement :

```solidity
function echidna_assert_after_f() public returns (bool) {
    f(..);
    return(condition);
}
```

Cependant, il y a quelques probl√®mes :

- Elle √©choue si `f` est d√©clar√©e comme `internal` ou `external`.
- On ne sait pas clairement quels arguments devraient √™tre utilis√©s pour appeler `f`.
- Si `f` revert, la propri√©t√© √©chouera.

En g√©n√©ral, nous recommandons de suivre [la recommandation de John Regehr](https://blog.regehr.org/archives/1091) sur la fa√ßon d'utiliser les assertions :

- Ne forcez aucun effet de bord lors de la v√©rification de l'assertion. Par exemple : `assert(ChangeStateAndReturn() == 1)`
- Ne faites pas d'assertions sur des d√©clarations √©videntes. Par exemple `assert(var >= 0)` o√π `var` est d√©clar√© en tant que `uint`.

Enfin, veuillez **ne pas utiliser** `require` √† la place d'`assert`, car Echidna ne sera pas en mesure de le d√©tecter (mais le contrat sera annul√© de toute fa√ßon).

### R√©sum√© : V√©rification d'assertion {#summary-assertion-checking}

Ce qui suit r√©sume l'ex√©cution d'Echidna sur notre exemple :

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    assert (tmp <= counter);
    return (counter - tmp);
  }
}
```

```bash
echidna-test assert.sol --config config.yaml
Analyzing contract: assert.sol:Incrementor
assertion in inc: failed!üí•
  Call sequence, shrinking (2596/5000):
    inc(21711016731996786641919559689128982722488122124807605757398297001483711807488)
    inc(7237005577332262213973186563042994240829374041602535252466099000494570602496)
    inc(86844066927987146567678238756515930889952488499230423029593188005934847229952)

Seed: 1806480648350826486
```

Echidna a d√©couvert que l'assertion dans `inc` peut √©chouer si cette fonction est appel√©e plusieurs fois avec de grands arguments.

## Collecter et modifier un corpus Echidna {#collecting-and-modifying-an-echidna-corpus}

Nous verrons comment collecter et utiliser un corpus de transactions avec Echidna. La cible est le contrat intelligent suivant [`magic.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/magic.sol) :

```solidity
contract C {
  bool value_found = false;
  function magic(uint magic_1, uint magic_2, uint magic_3, uint magic_4) public {
    require(magic_1 == 42);
    require(magic_2 == 129);
    require(magic_3 == magic_4+333);
    value_found = true;
    return;
  }

  function echidna_magic_values() public returns (bool) {
    return !value_found;
  }

}
```

Ce simple exemple oblige Echidna √† trouver certaines valeurs pour changer une variable d'√©tat. C'est difficile pour un fuzzer
(il est recommand√© d'utiliser un outil d'ex√©cution symbolique comme [Manticore](https://github.com/trailofbits/manticore)).
Nous pouvons ex√©cuter Echidna pour v√©rifier cela :

```bash
echidna-test magic.sol
...

echidna_magic_values: passed! üéâ

Seed: 2221503356319272685
```

Cependant, nous pouvons toujours utiliser Echidna pour collecter le corpus lors de l'ex√©cution de cette campagne de fuzzing.

### Collecte d'un corpus {#collecting-a-corpus}

Pour activer la collecte de corpus, cr√©ez un r√©pertoire de corpus :

```bash
mkdir corpus-magic
```

Et un [fichier de configuration Echidna](https://github.com/crytic/echidna/wiki/Config) `config.yaml` :

```yaml
coverage: true
corpusDir: "corpus-magic"
```

Maintenant nous pouvons ex√©cuter notre outil et v√©rifier le corpus collect√© :

```bash
echidna-test magic.sol --config config.yaml
```

Echidna ne peut toujours pas trouver les bonnes valeurs magiques, mais nous pouvons jeter un ≈ìil sur le corpus qu'il a collect√©.
Par exemple, l'un de ces fichiers √©tait :

```json
[
  {
    "_gas'": "0xffffffff",
    "_delay": ["0x13647", "0xccf6"],
    "_src": "00a329c0648769a73afac7f9381e08fb43dbea70",
    "_dst": "00a329c0648769a73afac7f9381e08fb43dbea72",
    "_value": "0x0",
    "_call": {
      "tag": "SolCall",
      "contents": [
        "magic",
        [
          {
            "contents": [
              256,
              "93723985220345906694500679277863898678726808528711107336895287282192244575836"
            ],
            "tag": "AbiUInt"
          },
          {
            "contents": [256, "334"],
            "tag": "AbiUInt"
          },
          {
            "contents": [
              256,
              "68093943901352437066264791224433559271778087297543421781073458233697135179558"
            ],
            "tag": "AbiUInt"
          },
          {
            "tag": "AbiUInt",
            "contents": [256, "332"]
          }
        ]
      ]
    },
    "_gasprice'": "0xa904461f1"
  }
]
```

√âvidemment, cette entr√©e ne d√©clenchera pas l'√©chec de notre propri√©t√©. Cependant, au cours de la prochaine √©tape, nous verrons comment la modifier en ce sens.

### Amorcer un corpus {#seeding-a-corpus}

Echidna a besoin d'aide pour g√©rer la fonction `magic`. Nous allons copier et modifier l'entr√©e pour utiliser des param√®tres appropri√©s
pour elle :

```bash
cp corpus/2712688662897926208.txt corpus/new.txt
```

Nous allons modifier `new.txt` pour appeler `magic(42,129,333,0)`. Maintenant, nous pouvons relancer Echidna :

```bash
echidna-test magic.sol --config config.yaml
...
echidna_magic_values: failed!üí•
  Call sequence:
    magic(42,129,333,0)


Unique instructions: 142
Unique codehashes: 1
Seed: -7293830866560616537

```

Cette fois-ci, il a conclu imm√©diatement que la propri√©t√© a √©t√© compromise.

## Trouver les transactions √† forte consommation de gaz {#finding-transactions-with-high-gas-consumption}

Nous verrons comment trouver avec Echida les transactions √† forte consommation de gaz. La cible est le contrat intelligent suivant :

```solidity
contract C {
  uint state;

  function expensive(uint8 times) internal {
    for(uint8 i=0; i < times; i++)
      state = state + i;
  }

  function f(uint x, uint y, uint8 times) public {
    if (x == 42 && y == 123)
      expensive(times);
    else
      state = 0;
  }

  function echidna_test() public returns (bool) {
    return true;
  }

}
```

Ici, `expensive` peut avoir une grande consommation de gaz.

Actuellement, Echidna a toujours besoin d'une propri√©t√© pour tester : ici, `echidna_test` renvoie toujours `true`.
Nous pouvons ex√©cuter Echidna pour v√©rifier cela :

```
echidna-test gas.sol
...
echidna_test: passed! üéâ

Seed: 2320549945714142710
```

### Mesurer la consommation de gaz {#measuring-gas-consumption}

Pour activer la consommation de gaz avec Echidna, cr√©ez un fichier de configuration `config.yaml` :

```yaml
estimateGas: true
```

Dans cet exemple, nous allons √©galement r√©duire la taille de la s√©quence de transaction pour faciliter la compr√©hension des r√©sultats :

```yaml
seqLen: 2
estimateGas: true
```

### Ex√©cuter Echidna {#run-echidna-3}

Une fois que nous avons cr√©√© le fichier de configuration, nous pouvons ex√©cuter Echidna comme ceci :

```bash
echidna-test gas.sol --config config.yaml
...
echidna_test: passed! üéâ

f used a maximum of 1333608 gas
  Call sequence:
    f(42,123,249) Gas price: 0x10d5733f0a Time delay: 0x495e5 Block delay: 0x88b2

Unique instructions: 157
Unique codehashes: 1
Seed: -325611019680165325

```

- Le gaz indiqu√© est une estimation fournie par [HEVM](https://github.com/dapphub/dapptools/tree/master/src/hevm#hevm-).

### Filtrer les appels qui r√©duisent le gaz {#filtering-out-gas-reducing-calls}

Le tutoriel ci-dessus sur le **filtrage des fonctions √† appeler pendant une campagne de fuzzing** montre comment
supprimer certaines fonctions de vos tests.  
Cela peut √™tre essentiel pour obtenir une estimation pr√©cise du gaz.
Prenons l'exemple suivant :

```solidity
contract C {
  address [] addrs;
  function push(address a) public {
    addrs.push(a);
  }
  function pop() public {
    addrs.pop();
  }
  function clear() public{
    addrs.length = 0;
  }
  function check() public{
    for(uint256 i = 0; i < addrs.length; i++)
      for(uint256 j = i+1; j < addrs.length; j++)
        if (addrs[i] == addrs[j])
          addrs[j] = address(0x0);
  }
  function echidna_test() public returns (bool) {
      return true;
  }
}
```

Si Echidna peut appeler toutes les fonctions, il ne trouvera pas facilement les transactions avec un co√ªt de gaz √©lev√© :

```
echidna-test pushpop.sol --config config.yaml
...
pop used a maximum of 10746 gas
...
check used a maximum of 23730 gas
...
clear used a maximum of 35916 gas
...
push used a maximum of 40839 gas
```

C'est parce que le co√ªt d√©pend de la taille de `addrs` et que les appels al√©atoires ont tendance √† laisser le tableau presque vide.
Mettre sur liste noire `pop` et `clear` nous donne de bien meilleurs r√©sultats :

```yaml
filterBlacklist: true
filterFunctions: ["pop", "clear"]
```

```
echidna-test pushpop.sol --config config.yaml
...
push used a maximum of 40839 gas
...
check used a maximum of 1484472 gas
```

### R√©sum√© : Trouver les transactions √† forte consommation de gaz {#summary-finding-transactions-with-high-gas-consumption}

Echidna peut trouver des transactions avec une forte consommation de gaz en utilisant l'option de configuration `estimateGas` :

```yaml
estimateGas: true
```

```bash
echidna-test contract.sol --config config.yaml
...
```

Echidna rapportera une s√©quence avec la consommation maximale de gaz pour chaque fonction, une fois que la campagne de fuzzing est termin√©e.
