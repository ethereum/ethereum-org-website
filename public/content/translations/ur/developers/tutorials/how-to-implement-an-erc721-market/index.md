---
title: ERC-721 مارکیٹ کو کیسے نافذ کریں
description: ایک غیر مرکزی کلاسیفائیڈ بورڈ پر ٹوکنائزڈ آئٹمز کو فروخت کے لیے کیسے پیش کریں
author: "Alberto Cuesta Cañada"
tags: [ "اسمارٹ معاہدات", "erc-721", "solidity", "tokens" ]
skill: intermediate
lang: ur-in
published: 2020-03-19
source: Hackernoon
sourceUrl: https://hackernoon.com/how-to-implement-an-erc721-market-1e1a32j9
---

اس مضمون میں، میں آپ کو دکھانے جا رہا ہوں کہ Ethereum بلاک چین کے لیے Craigslist کو کیسے کوڈ کیا جائے۔

Gumtree، Ebay اور Craigslist سے پہلے، کلاسیفائیڈ بورڈز زیادہ تر کارک یا کاغذ کے بنے ہوتے تھے۔ اسکول کی راہداریوں، اخبارات، اسٹریٹ لائٹس، اور اسٹور فرنٹ پر کلاسیفائیڈ بورڈز لگے ہوتے تھے۔

انٹرنیٹ کے آنے سے یہ سب کچھ بدل گیا۔ ایک مخصوص کلاسیفائیڈ بورڈ کو دیکھنے والے لوگوں کی تعداد کئی گنا بڑھ گئی۔ اس کے ساتھ، وہ بازار جن کی وہ نمائندگی کرتے تھے، بہت زیادہ موثر ہو گئے اور عالمی سطح تک پھیل گئے۔ Ebay ایک بہت بڑا کاروبار ہے جس کی ابتدا ان فزیکل کلاسیفائیڈ بورڈز سے ہوتی ہے۔

بلاک چین کے ساتھ یہ بازار ایک بار پھر بدلنے کے لیے تیار ہیں، میں آپ کو دکھاتا ہوں کہ کیسے۔

## منیٹائزیشن {#monetization}

ایک عوامی بلاک چین کلاسیفائیڈ بورڈ کے کاروباری ماڈل کو Ebay اور کمپنی کے ماڈل سے مختلف ہونا پڑے گا۔

سب سے پہلے، [غیر مرکزیت کا زاویہ](/developers/docs/web2-vs-web3/) ہے۔ موجودہ پلیٹ فارمز کو اپنے سرورز کو برقرار رکھنے کی ضرورت ہوتی ہے۔ ایک غیر مرکزی پلیٹ فارم کو اس کے صارفین کے ذریعے برقرار رکھا جاتا ہے، اس لیے بنیادی پلیٹ فارم کو چلانے کی لاگت پلیٹ فارم کے مالک کے لیے صفر ہو جاتی ہے۔

پھر فرنٹ اینڈ ہے، یعنی وہ ویب سائٹ یا انٹرفیس جو پلیٹ فارم تک رسائی فراہم کرتا ہے۔ یہاں بہت سے اختیارات ہیں۔ پلیٹ فارم کے مالکان رسائی کو محدود کر سکتے ہیں اور ہر کسی کو اپنا انٹرفیس استعمال کرنے پر مجبور کر سکتے ہیں، جس کے لیے وہ ایک فیس وصول کرتے ہیں۔ پلیٹ فارم کے مالکان رسائی کو کھلا رکھنے کا فیصلہ بھی کر سکتے ہیں (طاقت عوام کے ہاتھ میں!) اور کسی کو بھی پلیٹ فارم کے لیے انٹرفیس بنانے کی اجازت دے سکتے ہیں۔ یا مالکان ان انتہاؤں کے درمیان کوئی بھی طریقہ اختیار کرنے کا فیصلہ کر سکتے ہیں۔

_مجھ سے زیادہ وژن رکھنے والے کاروباری رہنما یہ جانتے ہوں گے کہ اسے کیسے منیٹائز کیا جائے۔_ _میں صرف یہ دیکھتا ہوں کہ یہ موجودہ صورتحال سے مختلف ہے اور شاید منافع بخش بھی ہے۔_

مزید برآں، آٹومیشن اور ادائیگیوں کا زاویہ بھی ہے۔ کچھ چیزوں کو بہت [مؤثر طریقے سے ٹوکنائزڈ](https://hackernoon.com/tokenization-of-digital-assets-g0ffk3v8s?ref=hackernoon.com) کیا جا سکتا ہے اور ایک کلاسیفائیڈ بورڈ پر ان کی تجارت کی جا سکتی ہے۔ ٹوکنائزڈ اثاثوں کو بلاک چین میں آسانی سے منتقل کیا جا سکتا ہے۔ انتہائی پیچیدہ ادائیگی کے طریقوں کو بلاک چین میں آسانی سے نافذ کیا جا سکتا ہے۔

مجھے یہاں کاروبار کا ایک موقع نظر آ رہا ہے۔ بغیر کسی چلانے کی لاگت والا ایک کلاسیفائیڈ بورڈ آسانی سے نافذ کیا جا سکتا ہے، جس میں ہر لین دین میں پیچیدہ ادائیگی کے راستے شامل ہوں۔ مجھے یقین ہے کہ کوئی نہ کوئی اس بارے میں ایک خیال لے کر آئے گا کہ اسے کس لیے استعمال کیا جائے۔

میں تو بس اسے بنا کر خوش ہوں۔ آئیے کوڈ پر ایک نظر ڈالتے ہیں۔

## عمل درآمد {#implementation}

کچھ عرصہ پہلے ہم نے کاروباری کیس کی مثالوں کے نفاذ اور دیگر اچھی چیزوں کے ساتھ ایک [اوپن سورس ریپوزٹری](https://github.com/HQ20/contracts?ref=hackernoon.com) شروع کی تھی، براہ کرم اس پر ایک نظر ڈالیں۔

اس [Ethereum کلاسیفائیڈز بورڈ](https://github.com/HQ20/contracts/tree/master/contracts/classifieds?ref=hackernoon.com) کا کوڈ وہیں ہے، براہ کرم اسے استعمال کریں اور اس کا بھرپور فائدہ اٹھائیں۔ بس اس بات سے آگاہ رہیں کہ کوڈ کا آڈٹ نہیں کیا گیا ہے اور اس میں پیسہ لگانے سے پہلے آپ کو اپنی خود کی جانچ پڑتال کرنے کی ضرورت ہے۔

بورڈ کی بنیادی باتیں پیچیدہ نہیں ہیں۔ بورڈ میں موجود تمام اشتہارات صرف چند فیلڈز کے ساتھ ایک اسٹرکٹ ہوں گے:

```solidity
struct Trade {
  address poster;
  uint256 item;
  uint256 price;
  bytes32 status; // کھلا، عملدرآمد، منسوخ
}
```

تو کوئی ہے جو اشتہار پوسٹ کر رہا ہے۔ فروخت کے لیے ایک آئٹم۔ آئٹم کی قیمت۔ ٹریڈ کی حیثیت جو کہ کھلی، عملدرآمد شدہ یا منسوخ ہو سکتی ہے۔

یہ تمام ٹریڈز ایک میپنگ میں رکھے جائیں گے۔ کیونکہ Solidity میں ہر چیز ایک میپنگ ہی لگتی ہے۔ اس لیے بھی کہ یہ آسان ہے۔

```solidity
mapping(uint256 => Trade) public trades;
```

میپنگ کا استعمال کرنے کا صرف یہ مطلب ہے کہ ہمیں ہر اشتہار کو پوسٹ کرنے سے پہلے اس کے لیے ایک آئی ڈی بنانی ہوگی، اور اس پر کام کرنے سے پہلے ہمیں اشتہار کی آئی ڈی جاننے کی ضرورت ہوگی۔ اس سے نمٹنے کے متعدد طریقے ہیں، چاہے وہ اسمارٹ کنٹریکٹ میں ہوں یا فرنٹ اینڈ میں۔ اگر آپ کو کچھ اشارے چاہئیں تو براہ کرم پوچھیں۔

اگلا سوال یہ پیدا ہوتا ہے کہ وہ کون سی اشیاء ہیں جن کا ہم لین دین کرتے ہیں، اور وہ کون سی کرنسی ہے جو لین دین کی ادائیگی کے لیے استعمال ہوتی ہے۔

آئٹمز کے لیے، ہم صرف یہ مطالبہ کرنے جا رہے ہیں کہ وہ [ERC-721](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/IERC721.sol?ref=hackernoon.com) انٹرفیس کو نافذ کریں، جو واقعی بلاک چین میں حقیقی دنیا کی اشیاء کی نمائندگی کرنے کا ایک طریقہ ہے، حالانکہ یہ [ڈیجیٹل اثاثوں کے ساتھ بہترین کام کرتا ہے](https://hackernoon.com/tokenization-of-digital-assets-g0ffk3v8s?ref=hackernoon.com)۔ ہم کنسٹرکٹر میں اپنے ERC721 کنٹریکٹ کی وضاحت کرنے جا رہے ہیں، جس کا مطلب ہے کہ ہمارے کلاسیفائیڈ بورڈ میں موجود کسی بھی اثاثے کو پہلے سے ٹوکنائزڈ ہونا ضروری ہے۔

ادائیگیوں کے لیے، ہم کچھ ایسا ہی کرنے جا رہے ہیں۔ زیادہ تر بلاک چین پروجیکٹس اپنی خود کی [ERC-20](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/ERC20.sol?ref=hackernoon.com) کرپٹو کرنسی کی وضاحت کرتے ہیں۔ کچھ دوسرے DAI جیسی مرکزی دھارے کی کرنسی کا استعمال کرنا پسند کرتے ہیں۔ اس کلاسیفائیڈ بورڈ میں، آپ کو صرف تعمیر کے وقت یہ فیصلہ کرنا ہوگا کہ آپ کی کرنسی کیا ہوگی۔ آسان ہے۔

```solidity
constructor (
  address _currencyTokenAddress, address _itemTokenAddress
) public {
  currencyToken = IERC20(_currencyTokenAddress);
  itemToken = IERC721(_itemTokenAddress);
  tradeCounter = 0;
}
```

ہم وہاں پہنچ رہے ہیں۔ ہمارے پاس اشتہارات، تجارت کے لیے آئٹمز اور ادائیگیوں کے لیے ایک کرنسی ہے۔ اشتہار بنانے کا مطلب ہے کسی آئٹم کو ایسکرو میں رکھنا تاکہ یہ دکھایا جا سکے کہ آپ کے پاس وہ آئٹم ہے اور آپ نے اسے دو بار پوسٹ نہیں کیا ہے، ممکنہ طور پر کسی مختلف بورڈ پر۔

نیچے دیا گیا کوڈ بالکل یہی کرتا ہے۔ آئٹم کو ایسکرو میں رکھتا ہے، اشتہار بناتا ہے، کچھ ہاؤس کیپنگ کرتا ہے۔

```solidity
function openTrade(uint256 _item, uint256 _price)
  public
{
  itemToken.transferFrom(msg.sender, address(this), _item);
  trades[tradeCounter] = Trade({
    poster: msg.sender,
    item: _item,
    price: _price,
    status: "Open"
  });
  tradeCounter += 1;
  emit TradeStatusChange(tradeCounter - 1, "Open");
}
```

ٹریڈ کو قبول کرنے کا مطلب ہے ایک اشتہار (ٹریڈ) کا انتخاب کرنا، قیمت ادا کرنا، اور آئٹم وصول کرنا۔ نیچے دیا گیا کوڈ ایک ٹریڈ کو بازیافت کرتا ہے۔ یہ چیک کرتا ہے کہ آیا یہ دستیاب ہے۔ آئٹم کی قیمت ادا کرتا ہے۔ آئٹم کو بازیافت کرتا ہے۔ اشتہار کو اپ ڈیٹ کرتا ہے۔

```solidity
function executeTrade(uint256 _trade)
  public
{
  Trade memory trade = trades[_trade];
  require(trade.status == "Open", "ٹریڈ کھلی نہیں ہے۔");
  currencyToken.transferFrom(msg.sender, trade.poster, trade.price);
  itemToken.transferFrom(address(this), msg.sender, trade.item);
  trades[_trade].status = "Executed";
  emit TradeStatusChange(_trade, "Executed");
}
```

آخر میں، ہمارے پاس بیچنے والوں کے لیے ایک آپشن ہے کہ وہ کسی خریدار کے قبول کرنے سے پہلے ٹریڈ سے پیچھے ہٹ سکتے ہیں۔ کچھ ماڈلز میں، اشتہارات ختم ہونے سے پہلے ایک مدت کے لیے لائیو رہتے ہیں۔ یہ آپ کی پسند ہے، جو آپ کے بازار کے ڈیزائن پر منحصر ہے۔

یہ کوڈ ٹریڈ کو انجام دینے کے لیے استعمال ہونے والے کوڈ سے بہت ملتا جلتا ہے، صرف اتنا فرق ہے کہ اس میں کرنسی کا تبادلہ نہیں ہوتا اور آئٹم اشتہار پوسٹ کرنے والے کے پاس واپس چلا جاتا ہے۔

```solidity
function cancelTrade(uint256 _trade)
  public
{
  Trade memory trade = trades[_trade];
  require(
    msg.sender == trade.poster,
    "ٹریڈ کو صرف پوسٹر ہی منسوخ کر سکتا ہے۔"
  );
  require(trade.status == "Open", "ٹریڈ کھلی نہیں ہے۔");
  itemToken.transferFrom(address(this), trade.poster, trade.item);
  trades[_trade].status = "Cancelled";
  emit TradeStatusChange(_trade, "Cancelled");
}
```

بس یہی ہے۔ آپ نفاذ کے اختتام تک پہنچ گئے ہیں۔ یہ کافی حیران کن ہے کہ کچھ کاروباری تصورات کوڈ میں ظاہر ہونے پر کتنے جامع ہوتے ہیں، اور یہ ان میں سے ایک معاملہ ہے۔ مکمل کنٹریکٹ کو [ہمارے ریپو میں](https://github.com/HQ20/contracts/blob/master/contracts/classifieds/Classifieds.sol) چیک کریں۔

## نتیجہ {#conclusion}

کلاسیفائیڈ بورڈز ایک عام مارکیٹ کنفیگریشن ہیں جو انٹرنیٹ کے ساتھ بڑے پیمانے پر پھیلے، اور چند اجارہ دار فاتحین کے ساتھ ایک بہت مقبول کاروباری ماڈل بن گئے۔

کلاسیفائیڈ بورڈز ایک بلاک چین ماحول میں نقل کرنے کے لیے ایک آسان ٹول بھی ہیں، جن میں بہت مخصوص خصوصیات ہیں جو موجودہ بڑے اداروں کے لیے ایک چیلنج کو ممکن بنائیں گی۔

اس مضمون میں، میں نے ایک کلاسیفائیڈ بورڈ کاروبار کی کاروباری حقیقت اور تکنیکی نفاذ کے درمیان ایک پل بنانے کی کوشش کی ہے۔ اگر آپ کے پاس صحیح مہارتیں ہیں تو یہ علم آپ کو نفاذ کے لیے ایک وژن اور روڈ میپ بنانے میں مدد کرے گا۔

ہمیشہ کی طرح، اگر آپ کچھ دلچسپ بنانے جا رہے ہیں اور کچھ مشورہ چاہتے ہیں، تو براہ کرم [مجھ سے رابطہ کریں](https://albertocuesta.es/)! مجھے مدد کرنے میں ہمیشہ خوشی ہوتی ہے۔
