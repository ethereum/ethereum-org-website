---
title: Como usar o Echidna para testar contratos inteligentes
description: Como usar o Echidna para testar automaticamente contratos inteligentes
author: "Trailofbits"
lang: pt-br
tags:
  - "solidez"
  - "smart contracts"
  - "seguran√ßa"
  - "testando"
  - "fuzzing"
skill: advanced
published: 2020-04-10
source: Construindo contratos seguros
sourceUrl: https://github.com/crytic/building-secure-contracts/tree/master/program-analysis/echidna
---

## Instala√ß√£o {#installation}

Echidna pode ser instalado atrav√©s do docker ou usando o bin√°rio pr√©-compilado.

### Echidna com docker {#echidna-through-docker}

```bash
docker pull trailofbits/eth-security-toolbox
docker run -it -v "$PWD":/home/training trailofbits/eth-security-toolbox
```

_O √∫ltimo comando roda a eth-security-toolbox em um docker que tem acesso ao seu diret√≥rio atual. Voc√™ pode alterar os arquivos do seu host e executar as ferramentas nos arquivos atrav√©s do docker_

Dentro do docker, execute :

```bash
solc-select 0.5.11
cd /home/training
```

### Bin√°rio {#binary}

[https://github.com/crytic/echidna/releases/tag/v1.4.0.0](https://github.com/crytic/echidna/releases/tag/v1.4.0.0)

## Introdu√ß√£o a fuzzing baseado em propriedade {#introduction-to-property-based-fuzzing}

Echidna √© um fuzzer baseado em propriedades, descrevemos em nossos posts anteriores ([1](https://blog.trailofbits.com/2018/03/09/echidna-a-smart-fuzzer-for-ethereum/), [2](https://blog.trailofbits.com/2018/05/03/state-machine-testing-with-echidna/), [3](https://blog.trailofbits.com/2020/03/30/an-echidna-for-all-seasons/)).

### Fuzzing {#fuzzing}

[Fuzzing](https://wikipedia.org/wiki/Fuzzing) √© uma t√©cnica bem conhecida na comunidade de seguran√ßa. It consists of generating inputs that are more or less random to find bugs in the program. Fuzzers por software tradicional (como [AFL](http://lcamtuf.coredump.cx/afl/) ou [LibFuzzer](https://llvm.org/docs/LibFuzzer.html)) s√£o conhecidos por serem ferramentas eficientes para encontrar bugs.

Al√©m da gera√ß√£o aleat√≥ria de entradas, h√° muitas t√©cnicas e estrat√©gias para gerar bons inputs, incluindo:

- Obtenha feedback de cada execu√ß√£o e gera√ß√£o de guias usando-o. Por exemplo, se uma entrada rec√©m-gerada leva √† descoberta de um novo caminho, ele pode fazer sentido para gerar novas entradas fechadas a ele.
- Gera√ß√£o da entrada respeitando uma restri√ß√£o estrutural. Por exemplo, se a sua entrada contiver um cabe√ßalho com uma soma de verifica√ß√£o, far√° sentido deixar o difusor gerar uma entrada validando a soma de verifica√ß√£o.
- Usando entradas conhecidas para gerar novas entradas: se voc√™ tem acesso a um grande conjunto de dados de entrada v√°lida, seu difusor pode gerar novas entradas a partir deles, ao inv√©s de come√ßar sua gera√ß√£o do zero. Eles geralmente s√£o chamados de _seeds_.

### Fuzzing baseado em propriedade {#property-based-fuzzing}

Echidna pertence a uma fam√≠lia espec√≠fica de fuzzer: fuzzing baseada em propriedades fortemente inspirada pelo [QuickCheck](https://wikipedia.org/wiki/QuickCheck). Em contraste com o fuzzing cl√°ssico que tentar√° encontrar falhas, Echidna tentar√° quebrar invariantes definidos pelo usu√°rio.

Nos contratos inteligentes, invariantes s√£o fun√ß√µes Solidity, que podem representar qualquer estado incorreto ou inv√°lido que o contrato possa alcan√ßar, incluindo:

- Controle de acesso incorreto: quem ataca tornou-se o propriet√°rio do contrato.
- M√°quina de estado incorreta: os tokens podem ser transferidos enquanto o contrato √© pausado.
- Aritm√©tica incorreta: o usu√°rio pode passar abaixo do saldo e obter tokens gratuitos ilimitados.

### Testando uma propriedade com Echidna {#testing-a-property-with-echidna}

Veremos como testar um contrato inteligente com o Echidna. O alvo √© o seguinte contrato inteligente [`exemplo.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/token.sol):

```solidity
contract Token{
  mapping(address => uint) public balances;
  function airdrop() public{
      balances[msg.sender] = 1000;
  }
  function consume() public{
      require(balances[msg.sender]>0);
      balances[msg.sender] -= 1;
  }
  function backdoor() public{
      balances[msg.sender] += 1;
  }
}
```

Assumiremos que esse token deve ter as seguintes propriedades:

- Qualquer um pode ter no m√°ximo 1000 tokens
- O token n√£o pode ser transferido (n√£o √© um token ERC20)

### Escrever uma propriedade {#write-a-property}

Propriedades do Echidna s√£o fun√ß√µes de Solidity. Uma propriedade deve:

- Ter nenhum argumento
- Retornar `verdadeiro` se for bem sucedido
- Tenha seu nome come√ßando com `echidna`

Echidna ir√°:

- Gera automaticamente transa√ß√µes arbitr√°rias para testar a propriedade.
- Relata quaisquer transa√ß√µes que levem uma propriedade para retornar `` falso ou lan√ßar um erro.
- Descartar efeito lateral ao chamar uma propriedade (ou seja, se a propriedade altera uma vari√°vel de estado, ela √© descartada ap√≥s o teste)

A propriedade a seguir verifica que o "caller" n√£o possui mais do que 1000 tokens:

```solidity
function echidna_balance_under_1000() public view returns(bool){
      return balances[msg.sender] <= 1000;
}
```

Use heran√ßa para separar seu contrato de suas propriedades:

```solidity
contract TestToken is Token{
      function echidna_balance_under_1000() public view returns(bool){
            return balances[msg.sender] <= 1000;
      }
  }
```

[`token.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/token.sol) implementa a propriedade e herda do token.

### Iniciar um contrato {#initiate-a-contract}

Echidna precisa de um [constructor](/developers/docs/smart-contracts/anatomy/#constructor-functions) sem argumento. Se seu contrato precisa de uma inicializa√ß√£o espec√≠fica, voc√™ precisa faz√™-lo no construtor.

H√° alguns endere√ßos espec√≠ficos no Echidna:

- `0x00a329c0648769A73afAc7F9381E08FB43dBEA72` que chama o constructor.
- `0x10000`, `0x20000`, e `0x00a329C0648769a73afAC7F9381e08fb43DBEA70` que aleatoriamente chama as outras fun√ß√µes.

N√≥s n√£o precisamos de nenhuma inicializa√ß√£o espec√≠fica em nosso exemplo atual, como resultado, nosso construtor est√° vazio.

### Executando Echidna {#run-echidna}

Echidna foi lan√ßado com:

```bash
echidna-test contract.sol
```

Se o contract.sol cont√©m m√∫ltiplos contratos, voc√™ pode especificar o alvo:

```bash
echidna-test contract.sol --contract MyContract
```

### Resumo: Testando uma propriedade {#summary-testing-a-property}

O seguinte resumo √© a execu√ß√£o de Echidna no nosso exemplo:

```solidity
contract TestToken is Token{
    constructor() public {}
        function echidna_balance_under_1000() public view returns(bool){
          return balances[msg.sender] <= 1000;
        }
  }
```

```bash
echidna-test testtoken.sol --contract TestToken
...

echidna_balance_under_1000: failed!üí•
  Call sequence, shrinking (1205/5000):
    airdrop()
    backdoor()

...
```

Echidna descobriu que a propriedade √© violada se `backdoor` √© chamada.

## Filtrando fun√ß√µes para chamar durante uma campanha de fuzzing {#filtering-functions-to-call-during-a-fuzzing-campaign}

Veremos como filtrar as fun√ß√µes a serem "fuzzed". O alvo √© o seguinte contrato inteligente:

```solidity
contract C {
  bool state1 = false;
  bool state2 = false;
  bool state3 = false;
  bool state4 = false;

  function f(uint x) public {
    require(x == 12);
    state1 = true;
  }

  function g(uint x) public {
    require(state1);
    require(x == 8);
    state2 = true;
  }

  function h(uint x) public {
    require(state2);
    require(x == 42);
    state3 = true;
  }

 function i() public {
    require(state3);
    state4 = true;
  }

  function reset1() public {
    state1 = false;
    state2 = false;
    state3 = false;
    return;
  }

  function reset2() public {
    state1 = false;
    state2 = false;
    state3 = false;
    return;
  }

  function echidna_state4() public returns (bool) {
    return (!state4);
  }
}
```

Este pequeno exemplo for√ßa Echidna a encontrar uma determinada sequ√™ncia de transa√ß√µes para alterar uma vari√°vel de estado. Isso √© dif√≠cil para um fuzzer (√© recomendado usar uma ferramenta de execu√ß√£o simb√≥lica como [Manticore](https://github.com/trailofbits/manticore)). Podemos executar o Echidna para verificar isto:

```bash
echidna-test multi.sol
...
echidna_state4: passed! üéâ
Seed: -3684648582249875403
```

### Filtrando fun√ß√µes {#filtering-functions}

Echidna tem problemas para encontrar a sequ√™ncia correta para testar esse contrato, porque as duas fun√ß√µes de redefini√ß√£o (`reset1` e `reset2`) definir√£o todas as vari√°veis de estado como `false`. No entanto, podemos usar um recurso especial Echidna para ou para a lista negra redefinir a fun√ß√£o ou apenas para a lista branca `f`, `g`, `h` e `i` fun√ß√µes.

Para fun√ß√µes da lista negra, podemos usar esse arquivo de configura√ß√£o:

```yaml
filterBlacklist: true
filterFunctions: ["reset1", "reset2"]
```

Outra abordagem para as fun√ß√µes de filtro √© listar as fun√ß√µes na lista branca. Para fazer isso, podemos usar este arquivo de configura√ß√£o:

```yaml
filterBlacklist: false
filterFunctions: ["f", "g", "h", "i"]
```

- `filterBlacklist` √© `verdadeiro` por padr√£o.
- A filtragem ser√° executada apenas por nome (sem par√¢metros). Se voc√™ tiver `f()` e `f(uint256)`, o filtro `"f"` corresponder√° a ambas as fun√ß√µes.

### Executar Echidna {#run-echidna-1}

Para executar Echidna com um arquivo de configura√ß√£o `blacklist.yaml`:

```bash
echidna-test multi.sol --config blacklist.yaml
...
echidna_state4: failed!üí•
  Call sequence:
    f(12)
    g(8)
    h(42)
    i()
```

Echidna vai encontrar a sequ√™ncia de transa√ß√µes para falsificar a propriedade quase de forma mesquinha.

### Resumo: Filtrando fun√ß√µes {#summary-filtering-functions}

Echidna pode ser chamada na lista negra ou na lista branca durante uma campanha de fuzzing:

```yaml
filterBlacklist: true
filterFunctions: ["f1", "f2", "f3"]
```

```bash
echidna-test contract.sol --config config.yaml
...
```

Echidna inicia uma campanha de fuzzing em qualquer blacklist `f1`, `f2` e `f3` ou apenas chamando a eles, de acordo com o valor do booleano `filterBlacklist`.

## Como testar a asser√ß√£o de Solidity com Echidna {#how-to-test-soliditys-assert-with-echidna}

Neste breve tutorial, vamos mostrar como usar o Echidna para testar a verifica√ß√£o de asser√ß√£o em contratos. Vamos supor que tenhamos um contrato como este:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    // tmp <= counter
    return (counter - tmp);
  }
}
```

### Escreva uma asser√ß√£o {#write-an-assertion}

Queremos ter certeza de que `tmp` √© menor ou igual a `contador` depois de retornar a sua diferen√ßa. N√≥s poder√≠amos escrever uma propriedade de Echidna, mas precisaremos armazenar o valor de `tmp` em algum lugar. Em vez disso, poder√≠amos usar uma asser√ß√£o como esta:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    assert (tmp <= counter);
    return (counter - tmp);
  }
}
```

### Executando Echidna {#run-echidna-2}

Para habilitar o teste de falha de asser√ß√£o, crie um arquivo de configura√ß√£o [Echidna](https://github.com/crytic/echidna/wiki/Config) `config.yaml`:

```yaml
checkAsserts: true
```

Quando executamos este contrato em Echidna, obtemos os resultados esperados:

```bash
echidna-test assert.sol --config config.yaml
Analyzing contract: assert.sol:Incrementor
assertion in inc: failed!üí•
  Call sequence, shrinking (2596/5000):
    inc(21711016731996786641919559689128982722488122124807605757398297001483711807488)
    inc(7237005577332262213973186563042994240829374041602535252466099000494570602496)
    inc(86844066927987146567678238756515930889952488499230423029593188005934847229952)

Seed: 1806480648350826486
```

Como voc√™ pode ver, Echidna relata algumas falhas de afirma√ß√£o na fun√ß√£o `inc`. Adicionar mais de uma asser√ß√£o por fun√ß√£o √© poss√≠vel, mas Echidna n√£o pode dizer qual afirma√ß√£o falhou.

### Quando e como usar asser√ß√µes {#when-and-how-use-assertions}

As asser√ß√µes podem ser usadas como alternativas √†s propriedades expl√≠citas, se as condi√ß√µes a serem verificadas est√£o diretamente relacionadas com o uso correto de alguma opera√ß√£o `f`. Adicionar asser√ß√µes ap√≥s algum c√≥digo for√ßar√° que a verifica√ß√£o ocorra imediatamente ap√≥s sua execu√ß√£o:

```solidity
function f(..) public {
    // some complex code
    ...
    assert (condition);
    ...
}

```

Pelo contr√°rio, usando uma propriedade Echidna expl√≠cita ir√° executar transa√ß√µes aleatoriamente e n√£o h√° maneira f√°cil de aplicar exatamente quando elas ser√£o verificadas. Ainda √© poss√≠vel fazer esta solu√ß√£o alternativa:

```solidity
function echidna_assert_after_f() public returns (bool) {
    f(..);
    return(condition);
}
```

Entretanto, existem alguns problemas: %{issues}:

- Ele falha se `f` √© declarado como `interno` ou `externo`.
- N√£o est√° claro quais argumentos devem ser usados para chamar `f`.
- Se `f` reverter, a propriedade ir√° falhar.

Em geral, recomendamos seguir a recomenda√ß√£o de [John Regehr](https://blog.regehr.org/archives/1091) sobre como usar asser√ß√µes:

- N√£o force qualquer efeito colateral durante a verifica√ß√£o de asser√ß√£o. Por exemplo: `assert(ChangeStateAndReturn() == 1)`
- N√£o fa√ßa asser√ß√µes √≥bvias. Por exemplo, `assert(var >= 0)` onde `var` √© declarado como `uint`.

Finalmente, **n√£o use** `require` em vez de `assert`, j√° que Echidna n√£o ser√° capaz de detect√°-lo (mas o contrato ser√° revertido mesmo assim).

### Resumo: checando a asser√ß√£o {#summary-assertion-checking}

O seguinte resumo √© a execu√ß√£o de Echidna no nosso exemplo:

```solidity
contract Incrementor {
  uint private counter = 2**200;

  function inc(uint val) public returns (uint){
    uint tmp = counter;
    counter += val;
    assert (tmp <= counter);
    return (counter - tmp);
  }
}
```

```bash
echidna-test assert.sol --config config.yaml
Analyzing contract: assert.sol:Incrementor
assertion in inc: failed!üí•
  Call sequence, shrinking (2596/5000):
    inc(21711016731996786641919559689128982722488122124807605757398297001483711807488)
    inc(7237005577332262213973186563042994240829374041602535252466099000494570602496)
    inc(86844066927987146567678238756515930889952488499230423029593188005934847229952)

Seed: 1806480648350826486
```

Echidna percebeu que a asser√ß√£o em `inc` pode falhar se essa fun√ß√£o √© chamada v√°rias vezes com argumentos grandes.

## Coletando e modificando um corpus Echidna {#collecting-and-modifying-an-echidna-corpus}

Veremos como coletar e usar um corpus de transa√ß√µes com Echidna. O alvo √© o seguinte contrato inteligente [`exemplo.sol`](https://github.com/crytic/building-secure-contracts/blob/master/program-analysis/echidna/example/magic.sol):

```solidity
contract C {
  bool value_found = false;
  function magic(uint magic_1, uint magic_2, uint magic_3, uint magic_4) public {
    require(magic_1 == 42);
    require(magic_2 == 129);
    require(magic_3 == magic_4+333);
    value_found = true;
    return;
  }

  function echidna_magic_values() public returns (bool) {
    return !value_found;
  }

}
```

Este pequeno exemplo for√ßa Echidna a encontrar uma determinada sequ√™ncia de transa√ß√µes para alterar uma vari√°vel de estado. Isso √© dif√≠cil para um fuzzer (√© recomendado usar uma ferramenta de execu√ß√£o simb√≥lica como [Manticore](https://github.com/trailofbits/manticore)). Podemos executar o Echidna para verificar isto:

```bash
echidna-test magic.sol
...

echidna_magic_values: passed! üéâ

Seed: 2221503356319272685
```

No entanto, ainda podemos usar o Echidna para coletar corpus na condu√ß√£o desta campanha de fuzzing.

### Coletando um corpus {#collecting-a-corpus}

Para habilitar a cole√ß√£o de corpus, crie um diret√≥rio corpus:

```bash
mkdir corpus-magic
```

E um [arquivo de configura√ß√£o Echidna](https://github.com/crytic/echidna/wiki/Config) `config.yaml`:

```yaml
coverage: true
corpusDir: "corpus-magic"
```

Agora podemos rodar nossa ferramenta e checar o corpus coletado:

```bash
echidna-test magic.sol --config config.yaml
```

Echidna ainda n√£o conseguiu encontrar os valores m√°gicos corretos, mas podemos olhar para o corpus que ele coletou. Por exemplo, um desses arquivos foi:

```json
[
  {
    "_gas'": "0xffffffff",
    "_delay": ["0x13647", "0xccf6"],
    "_src": "00a329c0648769a73afac7f9381e08fb43dbea70",
    "_dst": "00a329c0648769a73afac7f9381e08fb43dbea72",
    "_value": "0x0",
    "_call": {
      "tag": "SolCall",
      "contents": [
        "magic",
        [
          {
            "contents": [
              256,
              "93723985220345906694500679277863898678726808528711107336895287282192244575836"
            ],
            "tag": "AbiUInt"
          },
          {
            "contents": [256, "334"],
            "tag": "AbiUInt"
          },
          {
            "contents": [
              256,
              "68093943901352437066264791224433559271778087297543421781073458233697135179558"
            ],
            "tag": "AbiUInt"
          },
          {
            "tag": "AbiUInt",
            "contents": [256, "332"]
          }
        ]
      ]
    },
    "_gasprice'": "0xa904461f1"
  }
]
```

Claramente, esse input n√£o causar√° falha em nossa propriedade. No entanto, no pr√≥ximo passo, veremos como modific√°-lo nesse sentido.

### Semeando um corpus {#seeding-a-corpus}

Echidna precisa de ajuda para lidar com a fun√ß√£o `m√°gica`. Vamos copiar e modificar a entrada para usar os par√¢metros adequados para ele:

```bash
cp corpus/2712688662897926208.txt corpus/new.txt
```

N√≥s iremos modificar `new.txt` para chamar `m√°gica(42,129,333,0)`. Agora, podemos reexecutar o Echidna:

```bash
echidna-test magic.sol --config config.yaml
...
echidna_magic_values: failed!üí•
  Call sequence:
    magic(42,129,333,0)


Unique instructions: 142
Unique codehashes: 1
Seed: -7293830866560616537

```

Desta vez, constatou que a propriedade √© violada imediatamente.

## Localizando transa√ß√µes com alto consumo de gas {#finding-transactions-with-high-gas-consumption}

Veremos como encontrar as transa√ß√µes com alto consumo de gas com o Echidna. O alvo √© o seguinte contrato inteligente:

```solidity
contract C {
  uint state;

  function expensive(uint8 times) internal {
    for(uint8 i=0; i < times; i++)
      state = state + i;
  }

  function f(uint x, uint y, uint8 times) public {
    if (x == 42 && y == 123)
      expensive(times);
    else
      state = 0;
  }

  function echidna_test() public returns (bool) {
    return true;
  }

}
```

Aqui `caro` pode ter um grande consumo de gas.

Atualmente, Echidna sempre precisa de uma propriedade para testar: aqui `echidna_test` sempre retorna `true`. Podemos executar o Echidna para verificar isto:

```
echidna-test gas.sol
...
echidna_test: passed! üéâ

Seed: 2320549945714142710
```

### Medi√ß√£o do consumo de gas {#measuring-gas-consumption}

Para habilitar o consumo de gas com Echidna, crie um arquivo de configura√ß√£o `config.yaml`:

```yaml
estimateGas: true
```

Neste exemplo, tamb√©m reduziremos o tamanho da sequ√™ncia de transa√ß√µes para facilitar a compreens√£o dos resultados:

```yaml
seqLen: 2
estimateGas: true
```

### Executando Echidna {#run-echidna-3}

Assim que tivermos o arquivo de configura√ß√£o criado, poderemos executar o Echidna assim:

```bash
echidna-test gas.sol --config config.yaml
...
echidna_test: passed! üéâ

f used a maximum of 1333608 gas
  Call sequence:
    f(42,123,249) Gas price: 0x10d5733f0a Time delay: 0x495e5 Block delay: 0x88b2

Unique instructions: 157
Unique codehashes: 1
Seed: -325611019680165325

```

- O gas mostrado √© um c√°lculo fornecido por [HEVM](https://github.com/dapphub/dapptools/tree/master/src/hevm#hevm-).

### Filtrando Chamadas com Redu√ß√£o de Gas {#filtering-out-gas-reducing-calls}

O tutorial sobre **fun√ß√µes de filtragem para chamar durante uma campanha de difus√£o** acima mostra como remover algumas fun√ß√µes de seu teste.  
Isso pode ser fundamental para obter uma estimativa de gas precisa. Considere o seguinte exemplo:

```solidity
contract C {
  address [] addrs;
  function push(address a) public {
    addrs.push(a);
  }
  function pop() public {
    addrs.pop();
  }
  function clear() public{
    addrs.length = 0;
  }
  function check() public{
    for(uint256 i = 0; i < addrs.length; i++)
      for(uint256 j = i+1; j < addrs.length; j++)
        if (addrs[i] == addrs[j])
          addrs[j] = address(0x0);
  }
  function echidna_test() public returns (bool) {
      return true;
  }
}
```

Se Echidna pode chamar todas as fun√ß√µes, ele n√£o encontrar√° facilmente transa√ß√µes com alto custo de gas:

```
echidna-test pushpop.sol --config config.yaml
...
pop used a maximum of 10746 gas
...
check used a maximum of 23730 gas
...
clear used a maximum of 35916 gas
...
push used a maximum of 40839 gas
```

Isso porque o custo depende do tamanho dos `addrs` e chamadas aleat√≥rias tendem a deixar o array quase vazio. Lista negra `pop` e `limpa`, no entanto, nos d√° resultados muito melhores:

```yaml
filterBlacklist: true
filterFunctions: ["pop", "clear"]
```

```
echidna-test pushpop.sol --config config.yaml
...
push used a maximum of 40839 gas
...
check used a maximum of 1484472 gas
```

### Localizando transa√ß√µes com alto consumo de g√°s {#summary-finding-transactions-with-high-gas-consumption}

Echidna pode encontrar transa√ß√µes com alto consumo de g√°s usando a op√ß√£o de configura√ß√£o `estimateGas`:

```yaml
estimateGas: true
```

```bash
echidna-test contract.sol --config config.yaml
...
```

Echidna ir√° relatar uma sequ√™ncia com o consumo m√°ximo de gas para cada fun√ß√£o, uma vez terminada a campanha de fuzzing.
